Fit Champagne Whale

medium

# DOS of `withdrawAllUnlockedToken()` in certain conditions

## Summary
The `withdrawAllUnlockedToken()` function is used for transferring all the user's unlocked tokens to his account.  However with the current implementation of the function in some cases, it takes > 30m gas and the tx reverts.
## Vulnerability Detail
The logic for calculating the total unlocked balances is not optimized, there are a lot of empty iterations. As a result when the user has >= 260 unlocked balances and tries to withdraw all of them via `withdrawAllUnlockedToken()` the tx fails with **Out of Gas**. As a result, the user is not able to withdraw all of the unlocked tokens.


```solidity
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender);.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
        while (limit < lockCount) {  //<---- inefficient iteration mechanism, leading to a lot of redundant iterations and huge gas usage
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount);  //<---- all the locks are fetched every time, instead of a subset of them(as the intention of the pagination mechanism)
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount;
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); 
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }
```
## Impact
Users are not able to withdraw all their unlocked tokens by calling `withdrawAllUnlockedToken()`, leading to stuck funds into the Lock.sol contract.
## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L415-L425

POC:  Paste the following code inside Withdraw.t.sol 
```solidity
    function testWithdrawAllOutOfGas() public{
        _stakingToken.mint(user1, 1000e18);
        
        // User stakes tokens
        uint256 NUM_STAKES_USER1 = 260; 
        vm.prank(user1);
        _stakingToken.approve(address(lock), 1e18 * NUM_STAKES_USER1);
        for (uint256 i = 0; i < NUM_STAKES_USER1; i++) {
            vm.prank(user1);
            lock.stake(1e18, user1, 0);
        }

        // allow the user to withdraw late
        for (uint256 i = 0; i < NUM_STAKES_USER1; i++) {
            vm.prank(user1);
            lock.exitLateById(i);
        }
        
        // Simulate time passage
        vm.warp(block.timestamp + 30); 

        //a user tries to withdraw all tokens
        uint256 gasBefore = gasleft();
        vm.prank(user1);
        lock.withdrawAllUnlockedToken();
        // lock.withdrawAllUnlockedToken_fixed();

        uint256 gasAfter = gasleft();
        uint256 gasUsed = gasBefore - gasAfter;

        // withdrawall fails with out of gas error
        console.log("WithdrawAll gas used: ", gasUsed);
        assertTrue(gasUsed > 30_000_000, "out of gas");
    }
```



## Tool used

Manual Review

## Recommendation

Optimize the iterations over the locked/unlocked balances. This optimization reduced the gas used **~21 times**
-

```diff
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
-       uint256 limit;
+       uint256 limit = 10; // 10 limit gave the least gas usage for me, dev team can try with other values if needed
        uint256 totalUnlocked;
        
-       while (limit < lockCount) {
+      while (page * limit < lockCount) {
-           LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
+          LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, limit ); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

-            limit += 10; // Moves to the next page of locks.
            page++;
        }
```

