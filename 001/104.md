Careful Snowy Whale

high

# Front-running `notifyUnseenReward()` to steal rewards from the protocol

## Summary

The `notifyUnseenReward()` function is used to distribute rewards to users in the protocol. However, this function can be front-run, allowing rewards to be stolen from the protocol.

## Vulnerability Detail
The purpose of the `notifyUnseenReward()` function is to update the reward data for a specific token with a new reward amount. The protocol can use this function to allocate rewards to users. 
```solidity
    function notifyUnseenReward(address[] memory _rewardTokens) external {
        uint256 length = rewardTokens.length; // Gets the number of reward tokens currently recognized by the contract.
        for (uint256 i = 0; i < length; ++i) {
            if (rewardTokenAdded[_rewardTokens[i]]) {
                _notifyUnseenReward(_rewardTokens[i]); // Processes each token to update any unseen rewards.
            }
        }
    }


```



When this function is called, the protocol updates the `cumulatedReward`, 
```solidity

    function _notifyReward(address _rewardToken, uint256 reward) internal {
        if (lockedSupplyWithMultiplier == 0)
            return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).

        Reward storage r = rewardData[_rewardToken]; // Accesses the reward structure for the specified token.
        uint256 newReward = reward * 1e36 / lockedSupplyWithMultiplier; // Calculates the reward per token, scaled up for precision.
        r.cumulatedReward += newReward; // Updates the cumulative reward for the token.
        r.lastUpdateTime = block.timestamp; // Sets the last update time to now.
        r.balance += reward; // Increments the balance of the token by the new reward amount.
    }

```

which is used to calculate user earnings, as shown in the code snippet below. 
```solidity

   function _earned(
        address _user,
        address _rewardToken
    ) internal view returns (uint256 earnings) {
        Reward memory rewardInfo = rewardData[_rewardToken]; // Retrieves reward data for the specified token.
        Balances memory balance = balances[_user]; // Retrieves balance information for the user.
        earnings = rewardInfo.cumulatedReward * balance.lockedWithMultiplier - rewardDebt[_user][_rewardToken]; // Calculates earnings by considering the accumulated reward and the reward debt.
    }
```

However, this process can be front-run. For example, a malicious user who notices that a large amount of rewards is about to be injected into the pool and is prepared to call `notifyUnseenReward()` for distribution can front-run this transaction by using high gas to call `stake()` for staking. The protocol will call `_updateReward()` to record the user's initial rewards and reward debt.
```solidity

    function _updateReward(address account) internal {
        uint256 length = rewardTokens.length; // Determine the number of reward tokens.
        Balances storage bal = balances[account]; // Access the balance record for the user.

        for (uint256 i = 0; i < length; ) {
            address token = rewardTokens[i]; // Access each token.
            Reward memory rewardInfo = rewardData[token]; // Get the reward data for the token.

            rewards[account][token] += _earned(account, token); // Update the rewards for the user based on what has been earned so far.
            rewardDebt[account][token] = rewardInfo.cumulatedReward * bal.lockedWithMultiplier; // Update the reward debt based on the latest reward information.

            unchecked {
                i++;
            }
        }
    }

```

After `notifyUnseenReward()` is executed, updating the `cumulatedReward`, the malicious user can then call `exitLateById()` and `getReward()`. In this function, the protocol will call `_updateReward()` again to update the rewards. 
```solidity
 function getReward(address[] memory _rewardTokens) public nonReentrant {
        _updateReward(msg.sender); // Updates any accrued rewards up to the current point for the caller.
        _getReward(msg.sender, _rewardTokens); // Calls the internal _getReward function to process the actual reward claim.
    }


```


Due to the difference in `cumulatedReward` before and after, the user will receive rewards.
```solidity
rewards[account][token] += _earned(account, token);
earnings = rewardInfo.cumulatedReward * balance.lockedWithMultiplier - rewardDebt[_user][_rewardToken]; // Calculates earnings by considering the accumulated reward and the reward debt.

```

## Impact
A bad actor can front-run the `notifyUnseenReward()` function to steal rewards from the protocol.





## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504-L511

## Tool used

Manual Review

## Recommendation

