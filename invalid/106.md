Careful Snowy Whale

medium

# The user may not be able to exit properly

## Summary
The calculation in `calcRemainUnlockPeriod()` is not precise. Even when the user's stake has reached the unlock time, the protocol might still calculate that more time is needed for unlocking. This can prevent the user from exiting the stake properly.


## Vulnerability Detail

In the `calcRemainUnlockPeriod()` function, if `lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod`, the function `calcRemainUnlockPeriod()` returns `lockPeriod - (block.timestamp - lockTime) % lockPeriod`. 
```solidity
    function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        
        if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
            return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
        } else {
            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
            return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
        }
    }
```

This check is not rigorous. If `lockPeriod <= defaultRelockTime` and `block.timestamp - lockTime > lockPeriod`, it indicates that the unlock time has already been reached. However, the protocol still follows the if condition to calculate the remaining unlock period. 

For example, if `defaultRelockTime = 30 days`, `lockPeriod = 20 days`, and `block.timestamp - lockTime = 25 days`, the protocol calculates the `RemainUnlockPeriod` as `20 - (25 % 20) = 15 days`. This result is incorrect because the lock has already expired. This causes users to be unable to exit normally.
```solidity
    function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
        locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }

```

## Impact
Users may be unable to exit properly.


## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L600-L603

## Tool used

Manual Review

## Recommendation
It's also necessary to check whether the unlock time has been reached.








