Energetic Gingham Mouse

high

# Manipulation of lockedSupplyWithMultiplier and totalSupply can lead to incorrect distribution of rewards

## Summary
The [`Lock.sol::exitLateById`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313-L313) function does not have sufficient validation to verify that the user has not called the same function yet.

Let's consider the execution of this function.
First, the function updates the user's rewards, then receives an object corresponding to the staking, updates the token release time,
then the most important thing
```solidity
uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;
```
Next, sets exitedLate true

There is no validation in this execution that the function has not been accessed before.
Let's look at how this can be used.

Let's assume this is the order of actions from the user

Steak 10 tokens. (multiplier 1)
```solidity
lockedSupplyWithMultiplier = 10;
lockedSupply = 10;
bal.lockedWithMultiplier = 10;
bal.locked = 10;
```
1 token steak (multiplier 1)
```solidity
lockedSupplyWithMultiplier = 11;
lockedSupply = 11;
bal.lockedWithMultiplier = 11;
bal.locked = 11;
```
Thus, the user has two stacks. The first has amount = 10, the second has amount = 1.

Now if the user will call `Lock.sol::exitLateById` for staking2 several times, he can change global variables `lockedSupplyWithMultiplier, lockedSupply`  to any value from 1 to 11. And not just on 1, 10, or 10 + 1, as it should be if the function could be called only once

This matters because lockedSupplyWithMultiplier is taken into account when calculating the distribution of rewards in the [_notifyReward](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L483-L483) function. By decreasing this parameter, the user can manipulate the rewards
```solidity
function _notifyReward(address _rewardToken, uint256 reward) internal {
        if (lockedSupplyWithMultiplier == 0)
            return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).

        Reward storage r = rewardData[_rewardToken]; // Accesses the reward structure for the specified token.
        uint256 newReward = reward * 1e36 / lockedSupplyWithMultiplier; // Calculates the reward per token, scaled up for precision.
        r.cumulatedReward += newReward; // Updates the cumulative reward for the token.
        r.lastUpdateTime = block.timestamp; // Sets the last update time to now.
        r.balance += reward; // Increments the balance of the token by the new reward amount.
    }
```

## Vulnerability Detail
Let's imagine that user 1 has made two steakings (100 tokens and 1 token (for simplicity, we assume that all steakings have a multiplier of 1). And user 2 has steaked 99 tokens.
Now, if they contribute rewards of 200 tokens, user 1 should get 101 and user 2 should get 99. 

User1 reward = (200 / (200 - x)) * (101 - x)
User2 reward = (200 / (200 - x)) * 99

Where X is the number of times user 1 called exitLateById for steaking with 1 token.

If user 1 calls exitLateById for the second steaking 101 times. Then user 1 will get 0 tokens and user 2 will get 198 tokens. 2 tokens are locked in the contract and will not be distributed. This is just an example of how rewards can be manipulated.

## Impact
Severity: Medium
Impact: High
Total: High
## Code Snippet
```solidity
function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
        locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }
```
## Tool used

Manual Review

## Recommendation
Add a check that the function has not already been called for a particular sterling