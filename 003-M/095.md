Blurry Orange Moose

high

# Early exit near the end of the lock-up period can be risky

## Summary

As the lock period nears its end, the penalty for early exit is close to the base penalty. However, immediately after the start of the new period, the penalty increases significantly.

## Vulnerability Detail

A penalty is applied within the `earlyExitById()` function.

```solidity
    function earlyExitById(uint256 lockId) external whenNotPaused {
        [...]
323     uint256 coolDownSecs = calcRemainUnlockPeriod(lock);
        lock.unlockTime = block.timestamp + coolDownSecs;
325     uint256 penaltyAmount = calcPenaltyAmount(lock);
        [...]
    }
```

```solidity
    function calcPenaltyAmount(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0; // Return zero if there is no amount locked to avoid unnecessary calculations.
        uint256 unlockTime = userLock.unlockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        uint256 penaltyFactor;


        if (lockPeriod <= defaultRelockTime || (block.timestamp - userLock.lockTime) < lockPeriod) {

            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriod + basePenaltyPercentage;
        }
        else {
            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / defaultRelockTime + basePenaltyPercentage;
        }

        // Apply the calculated penalty factor to the locked amount.
        penaltyAmount = userLock.amount * penaltyFactor / WHOLE;
    }
```

The penalty amount is determined by the `coolDownSecs`, which represents the remaining time until the end of the lock period. As shown in the `calcPenaltyAmount()` function, the penalty amount increases or decreases proportionally to the `(unlockTime - block.timestamp)`(`coolDownSecs`).

Just as the lock period is about to end, the `coolDownSecs` will be close to 0. Immediately after the start of the new period, the `coolDownSecs` will be close to the full period length. So, if a user invokes the `earlyExitById()` function right before the end of the lock-up period, they are likely to incur a small penalty. However, if the transaction is delayed even slightly, such that the new period has begun, the penalty will become significantly greater, leading to an unexpected outcome.

Additionally, if the owner resets the `defaultRelockTime` before the transaction is executed, the `coolDownSecs` value could change significantly. This could lead to the same undesirable outcome.

```solidity
    function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        
        if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
            return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
        } else {
            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
            return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
        }
    }
```

## Impact

If the `earlyExitById()` function is called near the end of the lock period, the user could be subjected to a significantly larger penalty due to several factors, such as the transaction being delayed or the `defaultRelockTime` being reset by the owner before the transaction executes.

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313-L343

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L569-L586

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L596-L607

## Tool used

Manual Review

## Recommendation

In the `earlyExitById()` function, it would be better to include a configurable parameter that allows users to set limitations, such as a maximum penalty or a deadline. For example:

```diff
-   function earlyExitById(uint256 lockId) external whenNotPaused {
+   function earlyExitById(uint256 lockId, uint256 maxPenalty) external whenNotPaused {
        if (isEarlyExitDisabled) {
            revert EarlyExitDisabled();
        }
        _updateReward(msg.sender);

        LockedBalance memory lock = locklist.getLockById(msg.sender, lockId);

        if (lock.unlockTime != 0)
            revert InvalidLockId();
        uint256 coolDownSecs = calcRemainUnlockPeriod(lock);
        lock.unlockTime = block.timestamp + coolDownSecs;
        uint256 penaltyAmount = calcPenaltyAmount(lock);
+       if (penaltyAmount > maxPenalty) revert("Unexpected Penalty!");
        locklist.removeFromList(msg.sender, lockId);
        Balances storage bal = balances[msg.sender];
        lockedSupplyWithMultiplier -= lock.amount * lock.multiplier;
        lockedSupply -= lock.amount;
        bal.locked -= lock.amount;
        bal.lockedWithMultiplier -= lock.amount * lock.multiplier;

        _updateRewardDebt(msg.sender);

        if (lock.amount > penaltyAmount) {
            IERC20(stakingToken).safeTransfer(msg.sender, lock.amount - penaltyAmount);
            IERC20(stakingToken).safeTransfer(treasury, penaltyAmount);
            emit EarlyExitById(lockId, msg.sender, lock.amount - penaltyAmount, penaltyAmount);
        } else {
            IERC20(stakingToken).safeTransfer(treasury, lock.amount);
        emit EarlyExitById(lockId, msg.sender, 0, penaltyAmount);
        }
    }
```