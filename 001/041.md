Skinny Rosewood Pangolin

medium

# Staker may receive much less staking token than expected when they exit early

## Summary
Staker may receive much less staking token than expected when they exit early, due to the uncertain execution time of the transaction.

## Vulnerability Detail
Protocol allows a staker to perform an early exit from a locked position by calling [earlyExitById(...)](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313) function and user will pay a penalty.

Based on the remaining time until the scheduled unlock, The penalty amount is computed as a percentage of the locked amount:
```solidity
        uint256 coolDownSecs = calcRemainUnlockPeriod(lock);
        lock.unlockTime = block.timestamp + coolDownSecs;
        uint256 penaltyAmount = calcPenaltyAmount(lock);
```

```solidity
    function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        
        if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
            return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
        } else {
            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
            return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
        }
    }
```
```solidity
    function calcPenaltyAmount(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0; // Return zero if there is no amount locked to avoid unnecessary calculations.
        uint256 unlockTime = userLock.unlockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        uint256 penaltyFactor;

        if (lockPeriod <= defaultRelockTime || (block.timestamp - userLock.lockTime) < lockPeriod) {
            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriod + basePenaltyPercentage;
        }
        else {
            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / defaultRelockTime + basePenaltyPercentage;
        }

        // Apply the calculated penalty factor to the locked amount.
        penaltyAmount = userLock.amount * penaltyFactor / WHOLE;
    }
```

For example, a user staked `1000e18` staking token for 30 days, at the 29th day, user calls **earlyExitById(...)** function to perform an early exit. 

According to [calcRemainUnlockPeriod(...)](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L600-L605) function, we can get the remaining unlock period is `1 days`:
> lockPeriod - (block.timestamp - lockTime) % lockPeriod => 30 days - 29 days % 30 days =  1 days

Then based on [calcPenaltyAmount(...)](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L576-L585) function, the penalty amount should be `160e18`:
> penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriod + basePenaltyPercentage = 1 days * 35% / 30 days + 15% = 16%
> penaltyAmount = userLock.amount * penaltyFactor / WHOLE = 1000e18 * 16% = 160e18

However, the transaction may be pending for a long time at gets executed within a new lock period, say the 31st day, if that is the case, the remaining unlock period is `29 days`:
> lockPeriod - (block.timestamp - lockTime) % lockPeriod => 30 days - 31 days % 30 days =  29 days

consequently, the penalty amount is `480e18`, much higher than expected.
> penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriod + basePenaltyPercentage = 29 days * 35% / 30 days + 15% = 48%
> penaltyAmount = userLock.amount * penaltyFactor / WHOLE = 1000e18 * 48% = 480e18

Please run the test codes below in  **PenaltyCalculations.t.sol** to verify:
```solidity
    function testAuditEarlyExit() public {
        uint256 balanceBeforeStaking = _stakingToken.balanceOf(user1);

        vm.prank(user1);
        lock.stake(1000e18, user1, 0);

        LockedBalance memory lockedBalance = lockList.getLock(user1, 0);
        // user1's lockPeriod is 10 secs
        assertEq(lockedBalance.lockPeriod, 10);
    
        // user1 submits to exit early at 9th sec but the transaction gets execution at the 11st sec (a new lock period)
        vm.warp(block.timestamp + 11);
        vm.prank(user1);
        lock.earlyExitById(lockedBalance.lockId);

        uint256 balanceAfterExiting = _stakingToken.balanceOf(user1);
        // user1 lost almost half of their staking
        assertEq(balanceBeforeStaking - balanceAfterExiting, 465e18);
    }
```

## Impact
Staker may receive much less staking token than expected when they exit early.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313

## Tool used
Manual Review

## Recommendation
Allow staker to specify the minimum amount of staking token they expect to receive:
```solidity
    function earlyExitById(uint256 lockId, uint256 minAmount) external whenNotPaused {
        ...

        if (lock.amount - penaltyAmount < minAmount) revert;

        ...
    }
```