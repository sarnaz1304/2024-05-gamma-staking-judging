Upbeat Graphite Gorilla

high

# Broken pagination mechanism at `withdrawAllUnlockedToken`

## Summary

The function `withdrawAllUnlockedToken` is in charge of getting all the locks from a user and withdrawing the funds from the locks that are already unlocked. To get all the locks from the user, the function uses a pagination mechanism to get the locks in batches of 10. However, this mechanism is broken and will cause the function to spend much more gas than intended, leading to an out-of-gas issue. 

## Vulnerability Detail

The function `withdrawAllUnlockedToken` is called when a user wants to withdraw the staked tokens from all of the possible locks. The function uses a pagination mechanism to get the user locks in batches of 10:

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L416
```solidity
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
        while (limit < lockCount) {
>>          LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount);
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount;
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId);
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }
    }
```

However, the pagination mechanism is broken because the function is passing the wrong argument to `locklist::getLocks`:

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/libraries/LockList.sol#L97-L110
```solidity
    function getLocks(
        address user,
        uint256 page,
        uint256 limit
    ) public view override returns (LockedBalance[] memory) {
        LockedBalance[] memory locks = new LockedBalance[](limit);
        uint256 lockIdsLength = lockIndexesByUser[user].length();

        uint256 i = page * limit;
        for (;i < (page + 1) * limit && i < lockIdsLength; i ++) {
            locks[i - page * limit]= lockById[lockIndexesByUser[user].at(i)];
        }
        return locks;
    }
```

The function `getLocks` receives an argument `limit` that's used to mark the limit of a page, this argument defines the length of the array that will be returned. Let's get some examples to understand the behavior of this function clearly:

- If `page` is 0 and `limit` is 10, the function will return an array of the first 10 locks from that user. 
- If `page` is 1 and `limit` is 10, the function will return an array of the locks 10-20 from that user. 
- If `page` is 1 and `limit` is 20, the function will return an array of the locks 20-40 from that user.


Knowing how the `getLocks` function works, we now can see why the mechanism is broken due to the wrong arguments given from `withdrawAllUnlockedToken`. 

```solidity
LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount);
```

Here we're passing as the `limit` argument the total locks of a user, therefore the returned array will be all the locks of a user. This is by itself pretty gas-expensive when there are many locks, but it gets worse given that this call is inside the `while` loop. 

Because the `while` loop is going to be executed as many times as `lockCount / 10` (due to the page length), this means that the full array of the user's locks is going to be queried multiple times, spending even more gas. 

For example, if a user has 100 locks and calls `withdrawAllUnlockedToken`, there are going to be 10 calls to `locklist::getLocks` and each one of them will return an array with 100 items, spending too much gas and probably have an out-of-gas issue. 

## Impact

When a user has many locks and calls `withdrawAllUnlockedToken`, the transaction will likely revert due to an out-of-gas issue caused by the broken pagination mechanism. Addresses like whales or smart contracts will probably trigger this bug if they create too many locks in the system. 

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L416

## Tool used

Manual Review

## Recommendation

There are some options to mitigate this issue:

1. Remove the while loop, which will make useless the pagination system but will considerably reduce the gas spent by the current broken system.
2. Fix the current pagination system to only query a page of locks for each `getLocks` call so that the function doesn't return huge arrays but only arrays with a predetermined page length (e.g. 10).
