Mini Rusty Coyote

medium

# Imprecise `penaltyAmount` calculations occurs inside the `Lock` contract due to the precision loss in `calcPenaltyAmount()` function

## Summary
Less accurate tokens are calculated and subtracted from `lock.amount` upon `earlyExitById()`due to division before multiplication in `calcPenaltyAmount()`

## Vulnerability Detail
Solidity rounds down the result of an integer division, and because of that, it is always recommended to multiply before
dividing to avoid that precision loss. In the case of a prior division over multiplication, the final result may face serious precision loss
as the first answer would face truncated precision and then multiplied to another integer.

The problem arises in Lock's `calcPenaltyAmount()` function. This function is responsible for calculating the `penaltyAmount` that user has to pay to protocol due to his decision to `earlyExitById()`. This amount is directly sent to the `treasury` of the protocol. Upon examining the implementation of `penaltyAmount` calculations, a concealed [division](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L578) occurs before [multiplication](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L585), resulting in the truncation of the entire expression. As you can see in the PoC below, this is problematic because the resulting precision loss can be substantial and leads to less accurate calculations. To understand better the bug, add this test suite contract to `test` file and run `forge test --mt testPenaltyPrecisionLoss -vvvv` : 
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.23;

import "forge-std/Test.sol";
import "../src/Lock.sol";
import "../src/mock/MockToken.sol";
import "../src/libraries/LockList.sol";


contract PoC is Test {
    Lock lock;
    LockList lockList;
    address deployer;
    address user1;
    address user2;
    address user3;
    MockToken _stakingToken;
    MockToken _rewardToken;

    uint256[] lockPeriod;
    uint256[] lockMultiplier;
    address[] rewardTokens;

    function setUp() public {
        user1 = vm.addr(1);
        user2 = vm.addr(2);
        user3 = vm.addr(3);
        deployer = vm.addr(9999999);
        uint256 forkId = vm.createFork("https://eth-mainnet.g.alchemy.com/v2/VZH1EtzO3RVWYSXs523zfhFzlO6KHnr6");
        vm.selectFork(forkId);

        vm.startPrank(deployer);
        lock = new Lock();
        lockList = new LockList(deployer);
        
        // Initialize the Lock contract with necessary configurations
        lock.initialize(address(lockList), 15_000, 35_000, deployer);
        _stakingToken = new MockToken("stakingToken", "stkToken", 6);
        _rewardToken = _stakingToken;
        rewardTokens.push(address(_rewardToken));

        // Configure staking token and reward token settings
        lock.addReward(address(_rewardToken));
        lock.setStakingToken(address(_stakingToken));
        lockPeriod.push(30 days);
        lockPeriod.push(270 days);
        lockPeriod.push(360 days);
        lockMultiplier.push(1);
        lockMultiplier.push(1);
        lockMultiplier.push(2);
        lock.setLockTypeInfo(lockPeriod, lockMultiplier);

        // Transfer ownership of locker lists to Lock contract
        _stakingToken.mint(user1, 5000000e18);
        _rewardToken.mint(deployer, 1000000e18);
        lock.setTreasury(address(0x4));
        lockList.transferOwnership(address(lock));
        vm.stopPrank();

        // Approve the Lock to manage staking tokens on behalf of users
        vm.prank(user1);
        _stakingToken.approve(address(lock), 1000000000e18);
        vm.prank(user2);
        _stakingToken.approve(address(lock), 10000e18);
        vm.prank(user3);
        _stakingToken.approve(address(lock), 10000e18);
        vm.prank(deployer);
        lock.setDefaultRelockTime(30 days);
    }

    function testPenaltyPrecisionLoss() public {
        // User 1 stakes some gamma
        vm.prank(user1);
        lock.stake(13931e18, user1, 0);

        //  Some time passed
        vm.warp(block.timestamp + 17 days + 1);

        LockedBalance memory lockedBalance = lockList.getLockById(user1, 0);
        uint256 cooldownSecs = lock.calcRemainUnlockPeriod(lockedBalance);
        lockedBalance.unlockTime = block.timestamp + cooldownSecs;
        
        // Precision loss method
        uint256 notAccurate = lock.calcPenaltyAmount(lockedBalance);
        // Accurate multiplication before division method
        uint256 accurate = calcPenaltyAmountRight(lockedBalance);

        console.log("Not accurate: ", notAccurate);
        console.log("Accurate    : ", accurate);

        // Protocol lost money
        assert(accurate > notAccurate);
    }

    // The right formula to calculate penaltyAmount with hardcoded values 
    function calcPenaltyAmountRight(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0;
        uint256 unlockTime = userLock.unlockTime; 
        uint256 lockPeriod = userLock.lockPeriod;

        if (lockPeriod <= 30 days || (block.timestamp - userLock.lockTime) < lockPeriod) {
            penaltyAmount = userLock.amount * (35_000 * (unlockTime - block.timestamp) + (lockPeriod * 15_000)) / (100_000 * lockPeriod);
        } else {
            penaltyAmount = userLock.amount * (35_000 * (unlockTime - block.timestamp) + (30 days * 15_000)) / (100_000 * 30 days);
        }
    }
}
```
> [!NOTE]
> Of course, the hardcoded values in `calcPenaltyAmountRight()` function are the same with the values in `calcPenaltyAmount()` but since they are in our test suite we have to hard code them. Nothing changes in the calculations.

## Impact
Precision loss inside the `calcPenaltyAmount()` function leads to less tokens sent to the `treasury` of the protocol and, therefore, loss of funds for Gamma.

## Code Snippet
Here is the vulnerable code snippet where the division before multiplication is done :
```solidity
    function calcPenaltyAmount(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0; // Return zero if there is no amount locked to avoid unnecessary calculations.
        uint256 unlockTime = userLock.unlockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        uint256 penaltyFactor;


        if (lockPeriod <= defaultRelockTime || (block.timestamp - userLock.lockTime) < lockPeriod) {
            // @audit Division is done here before multiplication which is done in penaltyAmount calculation
@>            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriod + basePenaltyPercentage;
        }
        else {
            // @audit Division is done here before multiplication which is done in penaltyAmount calculation
@>            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / defaultRelockTime + basePenaltyPercentage;
        }

        // Apply the calculated penalty factor to the locked amount.
@>        penaltyAmount = userLock.amount * penaltyFactor / WHOLE;
    }
```
[Link to code](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L569)

## Tool used
Manual code inspection

## Recommendation
Consider using this formula for calculating the penalty amount which is more accurate : 
```solidity
    function calcPenaltyAmountRight(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0;
        uint256 unlockTime = userLock.unlockTime; 
        uint256 lockPeriod = userLock.lockPeriod;

        if (lockPeriod <= defaultRelockTime || (block.timestamp - userLock.lockTime) < lockPeriod) {
            penaltyAmount = userLock.amount * (timePenaltyFraction * (unlockTime - block.timestamp) + (lockPeriod * basePenaltyPercentage)) / (WHOLE * lockPeriod);
        } else {
            penaltyAmount = userLock.amount * (timePenaltyFraction * (unlockTime - block.timestamp) + (defaultRelockTime * basePenaltyPercentage)) / (WHOLE * defaultRelockTime);
        }
    }
```
