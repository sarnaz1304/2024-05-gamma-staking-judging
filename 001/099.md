Uneven Admiral Urchin

high

# `withdrawUnlockedTokenById` allows users to steal rewards via reentrancy

## Summary
When users stake tokens, they can later call `exitLateById`, which allows them to withdraw their `locked tokens`.

They can also choose to `restake` their tokens instead of withdrawing, in case they mistakingly called `exitLateById`.

The problem is that a malicious user can withdraw their `locked tokens`, and before their lock is removed from the list, they can reenter and `restake`.

This will allow users to continue earning rewards for the `locked tokens` they already withdrew, without needing to transfer any `locked tokens`.

Users can repeat this process and drain funds/rewards from the protocol.

## Vulnerability Detail

After staking, users can call `exitLateById`
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313
```javascript
    /// @notice Allows a user to execute a late exit from a lock by specifying the lock ID, updating unlock times and reducing locked balances.
    /// @dev This function adjusts the unlock time based on the remaining cooldown period, updates the locked balances, flags the lock as exited late, and logs the exit.
    /// @param id The unique identifier of the lock from which the user wishes to exit late.
    function exitLateById(uint256 id) external { //@audit why is there no verification of timestamp?
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
        locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }
```

This now gives users two options:

1. Withdraw their locked tokens after `cooldown` period has passed
2. Restake their locked tokens

It's important to note that when the user chooses to `restake` their `locked tokens`, they do not have to transfer any new tokens, it simply reuses the `locked amount` already in the contract to restake.

User exploits this the following way:

User withdraws their locked tokens after `cooldown` period by calling `withdrawUnlockedTokenById`:
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L437
```javascript
    /// @notice Withdraws a specific unlocked token amount using the given lock ID, if the unlock time has passed.
    /// @dev Retrieves the lock details by ID, checks if it is unlocked, and transfers the unlocked amount to the user.
    /// @param id The unique identifier of the lock to check for unlocked tokens.
    function withdrawUnlockedTokenById(uint256 id) external nonReentrant {
        LockedBalance memory lockedBal = locklist.getLockById(msg.sender, id); // Retrieves the lock details for the specified ID.
        if (lockedBal.unlockTime != 0 && lockedBal.unlockTime < block.timestamp) {
@>          IERC20(stakingToken).safeTransfer(msg.sender, lockedBal.amount); // Transfers the unlocked amount to the user.
@>          locklist.removeFromList(msg.sender, id); // Removes the lock from the lock list.
            emit WithdrawUnlockedById(id, msg.sender, lockedBal.amount); // Emits an event logging the withdrawal of the unlocked tokens.
        }
    }
```

Since the respective `lock` is removed from the `locklist` after the transfer, the user can reenter when they receive the tokens, and call `restakeAfterLateExit`:
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L378
```javascript
    /// @notice Allows a user to restake funds after exiting late by mistake.
    /// @dev Enforces restrictions on the new lock period based on the current lock period and default relock time.
    /// @param id The ID of the lock that was exited late and needs to be re-staked.
    /// @param typeIndex The new lock type index to apply for the restake.
    function restakeAfterLateExit(uint256 id, uint256 typeIndex) external {
        // Retrieve the lock details for the specified ID.
@>      LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id);
        require(lockedBalance.exitedLate, "This lock was not exited late or is ineligible for restaking.");

        uint256 newLockPeriod = lockPeriod[typeIndex]; // Get the new lock period based on the type index.
        uint256 currentLockPeriod = lockedBalance.lockPeriod;

        // Enforce that the new lock period must be valid based on the current conditions.
        if (currentLockPeriod <= defaultRelockTime || (block.timestamp - lockedBalance.lockTime) < currentLockPeriod) {
            require(newLockPeriod >= currentLockPeriod, "New lock period must be greater than or equal to the current lock period");
        } else {
            require(newLockPeriod >= defaultRelockTime, "New lock period must be greater than or equal to the default relock time");
        }

        // Proceed to restake the funds using the new lock type.
@>      _stake(lockedBalance.amount, msg.sender, typeIndex, true);
        // after exiting, so they can restake without sending funds again

        // Remove the old lock record to prevent any further operations on it.
        locklist.removeFromList(msg.sender, id);

        emit RestakedAfterLateExit(msg.sender, id, lockedBalance.amount, typeIndex);
    }
```

Since the `lock` is not yet removed from the list, the checks will pass and the `locked amount` (that was already returned to the user) will be restaked.
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L260
```javascript
    function _stake(
        uint256 amount,
        address onBehalfOf,
        uint256 typeIndex,
        bool isRelock //@audit This is true if restaked
    ) internal whenNotPaused {

@>     if (!isRelock) {
            IERC20(stakingToken).safeTransferFrom(
                msg.sender,
                address(this),
                amount
            );
        }

        emit Locked(
            onBehalfOf,
            amount,
            balances[onBehalfOf].locked
        );
    }
```

Since `isRelock` is true, the user will not transfer any more locked tokens. They will continue to earn rewards for the same `locked amount` with a new `lock id`, despite already receiving a refund for the `locked amount`.

The old `lock` will then be removed from the list. It's important to note that `locklist.removeFromList` will still pass if the lock is already removed.

The user can repeat this process and drain funds/rewards from the contract.

## Impact
Funds/rewards drained from the `Lock` contract.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L437

## Tool used
Manual Review

## Recommendation
Remove the lock from the list prior to transferring the staking token to the user

```diff
    function withdrawUnlockedTokenById(uint256 id) external nonReentrant {
        LockedBalance memory lockedBal = locklist.getLockById(msg.sender, id); // Retrieves the lock details for the specified ID.
        if (lockedBal.unlockTime != 0 && lockedBal.unlockTime < block.timestamp) {
+           locklist.removeFromList(msg.sender, id); // Removes the lock from the lock list.
            IERC20(stakingToken).safeTransfer(msg.sender, lockedBal.amount); // Transfers the unlocked amount to the user.
-           locklist.removeFromList(msg.sender, id); // Removes the lock from the lock list.
            emit WithdrawUnlockedById(id, msg.sender, lockedBal.amount); // Emits an event logging the withdrawal of the unlocked tokens.
        }
    }
```