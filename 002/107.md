Round Coal Condor

medium

# The function withdrawAllUnlockedToken will fail when lockCount is large.

## Summary

The current implementation of the function withdrawAllUnlockedToken is wired. Page mode is not used when getting Locks, which will cause failure when lockCount is large.

## Vulnerability Detail

In the function withdrawAllUnlockedToken, all locks need to be obtained to check if they are unlocked. According to the implementation of function getLocks, its intention is to reduce memory usage through batch acquisition. However, in the current implementation of the function withdrawAllUnlockedToken, all locks are acquired at once. Through testing, it was found that when lockCount is large, the function withdrawAllUnlockedToken will revert.

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409-L430

```solidity
function withdrawAllUnlockedToken() external override nonReentrant {
    uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
    uint256 page;
    uint256 limit;
    uint256 totalUnlocked;

    while (limit < lockCount) {
        LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
        for (uint256 i = 0; i < lockedBals.length; i++) {
            if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
            }
        }

        limit += 10; // Moves to the next page of locks.
        page++;
    }

    IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
    emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
}

```

## POC

Withdraw.t.sol

```solidity
function testExitLateAndRewards() public {
    // Users stake tokens
    // vm.prank(user1);
    uint256 cnt = 5000;

    for (uint256 i=0; i<cnt; i++)
    {
        vm.prank(user1);
        lock.stake(1e15, user1, 0);
    }
            
    for (uint256 ii=0; ii<cnt; ii++)
    {
        vm.prank(user1);
        lock.exitLateById(ii);
    }
    vm.warp(block.timestamp + 3000000); // Simulate time passage
    
    // // Verify if the tokens are correctly unlocked and can be withdrawn
    uint256 user1BalanceBefore = _stakingToken.balanceOf(user1);
    vm.prank(user1);
    lock.withdrawAllUnlockedToken();
}
```

It will cause the EvmError.

## Impact

function withdrawAllUnlockedToken will revert.

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409-L430

## Tool used

Manual Review

## Recommendation

It is worth noting that changing the argument of getLocks from lockCount to 10 in the function withdrawAllUnlockedToken can avoid EvmError, but will cause a logic error because the remove operation is performed in the loop (the lockList is changed).

Here's an approach that I think is appropriate.

Use the fromIdx-step mode to retrieve the locks instead of page mode, which is more flexible.

For the fromIdx-step mode:

```solidity
    function getLocksFrom(
        address user,
        uint256 fromIdx,
        uint256 step
    ) public view override returns (LockedBalance[] memory) {
        LockedBalance[] memory locks = new LockedBalance[](step);
        uint256 lockIdsLength = lockIndexesByUser[user].length();

        uint256 i = fromIdx;
        for (;i < fromIdx + step && i < lockIdsLength; i ++) {
            locks[i - fromIdx]= lockById[lockIndexesByUser[user].at(i)];
        }
        return locks;
    }
```

And in the function withdrawAllUnlockedToken, when a lock is removed, fromIdx is minused by one.

```solidity
function withdrawAllUnlockedToken() external override nonReentrant {
    uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
    uint256 fromIdx;
    uint256 limit;
    uint256 totalUnlocked;
    uint256 step = 10;
    
    while (limit < lockCount) {
        LockedBalance[] memory lockedBals = locklist.getLocksFrom(msg.sender, fromIdx, step); // Retrieves a page of locks for the user.
        limit += step;
        fromIdx += step;
        for (uint256 i = 0; i < lockedBals.length; i++) {
            if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                fromIdx--;  // a lock is removed
            }
        }
    }

    IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
    emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
}
```