Shiny Hazel Cottonmouth

high

# Users can accumulate more rewards than they deserve after their locks period has passed

## Summary
After locking in the protocol, if an user chooses to not withdraw from the protocol, he will continuously accrue reward tokens even if his locked period has passed, giving him undeserved rewards.


## Vulnerability Detail
The internal `_updateReward` function updates user's reward and reward debt info whenever user initiates some actions. New rewards are added by the results of the [`_earned`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L455) function:

```solidity
    function _earned(
        address _user,
        address _rewardToken
    ) internal view returns (uint256 earnings) {
        Reward memory rewardInfo = rewardData[_rewardToken]; // Retrieves reward data for the specified token.
        Balances memory balance = balances[_user]; // Retrieves balance information for the user.
        earnings = rewardInfo.cumulatedReward * balance.lockedWithMultiplier - rewardDebt[_user][_rewardToken]; // Calculates earnings by considering the accumulated reward and the reward debt.
    }
```

where `rewardDebt` is set right after this function call:

```solidity
    function _updateReward(address account) internal {
        uint256 length = rewardTokens.length; // Determine the number of reward tokens.
        Balances storage bal = balances[account]; // Access the balance record for the user.

        for (uint256 i = 0; i < length; ) {
            address token = rewardTokens[i]; // Access each token.
            Reward memory rewardInfo = rewardData[token]; // Get the reward data for the token.

            rewards[account][token] += _earned(account, token); // Update the rewards for the user based on what has been earned so far.
            rewardDebt[account][token] = rewardInfo.cumulatedReward * bal.lockedWithMultiplier; // Update the reward debt based on the latest reward information.

            unchecked {
                i++;
            }
        }
    }
```

This is saying, when there are more rewards added to the protocol, or users have increased their staked balance, their accrued rewards should increase. `bal.lockedWithMultiplier` decreases when user chooses to unstake and withdraw from the protocol, and supposedly, users won't get reward updates. 

But one issue here is, users can still get reward even after their unlocking period, because the cumulated rewards will always be increasing, despite their locked amount stays constant, they can still get a share of more rewards as long as they don't exit from the protocol. This is incorrect because their reward should only be accrued when their stakes are active, if their unlocking period has passed, they should no longer receive more reward than what they already have.

The following PoC proves how this is possible:

```solidity
    function test_withdrawNoReward() public {
        // Users stake tokens
        vm.prank(user1);
        lock.stake(100e18, user1, 0);
        vm.prank(user2);
        lock.stake(100e18, user2, 1);
        vm.prank(user3);
        lock.stake(100e18, user3, 2);
        
        // Fetch the first lock for user1 and perform a late exit
        LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
        LockedBalance memory user1Lock = lockList.getLockById(user1, user1Locks[0].lockId);
        // Reward distribution
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 600e18);
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);

        vm.startPrank(user1);
        // lock.exitLateById(user1Lock.lockId);
        // vm.warp(block.timestamp + 30); // Simulate time passage
        uint256 debt = lock.getRewardDebt(user1, address(_rewardToken));
        console.log("debt:", debt);

        address[] memory tokens = new address[](0);
        lock.getReward(tokens);
        debt = lock.getRewardDebt(user1, address(_rewardToken));
        console.log("debt:", debt);

        uint256 user1Reward = lock.rewards(user1, address(_rewardToken));
        console.log("user1 reward:", user1Reward);

        lock.getAllRewards();

        uint256 rewardBalance1 = _rewardToken.balanceOf(user1);
        console.log("user1 reward balance", rewardBalance1);

        user1Reward = lock.rewards(user1, address(_rewardToken));
        console.log("user1 reward:", user1Reward);
        
        lock.stake(100e18, user1, 0);
        user1Reward = lock.rewards(user1, address(_rewardToken));
        console.log("user1 reward:", user1Reward);


        lock.getAllRewards();

        vm.stopPrank();

        rewardBalance1 = _rewardToken.balanceOf(user1);
        console.log("user1 reward balance", rewardBalance1);

        vm.prank(user2);
        lock.getReward(tokens);
        uint256 user2Reward = lock.rewards(user2, address(_rewardToken));
        console.log("user2 reward:", user2Reward);

        vm.prank(user3);
        lock.getReward(tokens);
        uint256 user3Reward = lock.rewards(user3, address(_rewardToken));
        console.log("user3 reward:", user3Reward);

        vm.warp(block.timestamp + 30);

        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 600e18);
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);

        vm.prank(user1);
        lock.getReward(tokens);
        user1Reward = lock.rewards(user1, address(_rewardToken));
        console.log("user1 reward:", user1Reward);

        vm.prank(user2);
        lock.getReward(tokens);
        user2Reward = lock.rewards(user2, address(_rewardToken));
        console.log("user2 reward:", user2Reward);

        vm.prank(user3);
        lock.getReward(tokens);
        user3Reward = lock.rewards(user3, address(_rewardToken));
        console.log("user3 reward:", user3Reward);

        vm.warp(block.timestamp + 90);

        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 600e18);
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);

        vm.prank(user1);
        lock.getReward(tokens);
        user1Reward = lock.rewards(user1, address(_rewardToken));
        console.log("user1 reward:", user1Reward);

        vm.prank(user2);
        lock.getReward(tokens);
        user2Reward = lock.rewards(user2, address(_rewardToken));
        console.log("user2 reward:", user2Reward);

        vm.prank(user3);
        lock.getReward(tokens);
        user3Reward = lock.rewards(user3, address(_rewardToken));
        console.log("user3 reward:", user3Reward);
    }
```

The result would show they will continously get more reward as more rewards are added to the protocol, even after their unlocking period.

## Impact
Some users can get more than they deserve, while making some users get less. This is equivalent of infinite relocking, as long as users don't exit and withdraw their locked assets.


## Code Snippet
```solidity
    function _earned(
        address _user,
        address _rewardToken
    ) internal view returns (uint256 earnings) {
        Reward memory rewardInfo = rewardData[_rewardToken]; // Retrieves reward data for the specified token.
        Balances memory balance = balances[_user]; // Retrieves balance information for the user.
        earnings = rewardInfo.cumulatedReward * balance.lockedWithMultiplier - rewardDebt[_user][_rewardToken]; // Calculates earnings by considering the accumulated reward and the reward debt.
    }
```

## Tool used

Manual Review, foundry

## Recommendation
Consider add a snapshot for rewards when rewards are added to the protocol, then calculate users' earning during their locked periods.
