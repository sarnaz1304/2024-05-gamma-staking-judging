Damaged Carbon Platypus

high

# High_01_WithdrawAllTokens_Broken_Pagination

## Summary
The `withdrawAllUnlockedToken` function in the `Lock` contract exhibits flawed pagination implementation, potentially rendering the function unusable due to high gas consumption. Moreover, the pagination mechanism within the `getLocks` function of the `LockList` contract also suffers from inefficiencies, returning unnecessary empty records. These critical issues significantly impact the contracts' functionality and efficiency, necessitating immediate corrective action.

## Vulnerability Detail
In `Lock:withdrawAllUnlockedToken`, pagination is incorrectly implemented, leading to excessive gas consumption. The current logic iterates over the entire array within each execution of the while loop, rendering pagination ineffective. `LockList::getLocks` utilizes `lockCount` instead of `limit` as a parameter, causing the function to iterate over the entire array unnecessarily, exacerbating gas inefficiency.

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409

## Impact
The flawed pagination implementation in both contracts severely hampers their usability and efficiency. The `withdrawAllUnlockedToken` function, considered a key functionality per documentation, risks becoming practically unusable due to high gas consumption. Additionally, the retrieval of unnecessary empty records in `getLocks` introduces inefficiencies, potentially hindering contract interactions and escalating transaction costs.

The severity of the vulnerability comes from the fact that both functions (`Lock::withdrawAllUnlockedToken` and `LockList::getLocks`) have problems with pagination. In combination the function is unusable due to high gas costs.

## Code Snippet
```solidity
function withdrawAllUnlockedToken() external override nonReentrant {
	uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
	uint256 page;
	uint256 limit;
	uint256 totalUnlocked;
	while (limit < lockCount) {
		LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
		for (uint256 i = 0; i < lockedBals.length; i++) {
			if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
				totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
				locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
			}
		}
	
		limit += 10; // Moves to the next page of locks.
		page++;
	}
	
	IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
	emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
}
```

## Tool used
Manual Review

## Recommendation
Consider directly iterating on all locks just once, without using while cycle as shown below:

```diff
function withdrawAllUnlockedToken() external override nonReentrant {
	uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
	
-   uint256 page;
-   uint256 limit;
	uint256 totalUnlocked;
	
+   LockedBalance[] memory lockedBals = locklist.getLocks(	
+      msg.sender,	
+      0,	
+      lockCount	
+   );
	
+   for (uint256 i = 0; i < lockedBals.length; i++) {
+      if (
+         lockedBals[i].unlockTime != 0 &&
+         lockedBals[i].unlockTime < block.timestamp
+      ) {
+      totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
+      locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
+      }
+   }
	
-   while (limit < lockCount) {
-      LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
-      for (uint256 i = 0; i < lockedBals.length; i++) {
-         if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
-            totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
-            locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
-         }
-      }
-      limit += 10; // Moves to the next page of locks.
-      page++;
-   }
	IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
	
	emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
}
```

## Proof of concept

1. Add this test to `Withdraw.t.sol`:

```solidity
function testWithrdaw_StakedTokens() public {
	vm.prank(user1);	
	lock.stake(100e18, user1, 0);
	uint256 i;
	
	for (i; i < 50; i++) {
		vm.prank(user1);
		lock.stake(1, user1, 0);
	}
	
	lock.withdrawAllUnlockedToken();
}
```

2. After running this command you can see the expected gas consumption `forge test --match-test testWithrdaw_StakedTokens -vvv --gas-report`

3. Then you can change the implementation to the proprosed one in the previous point `Recomendation` and run again the above command and see the consumed gas.