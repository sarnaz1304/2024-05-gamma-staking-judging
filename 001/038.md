Slow Sand Monkey

medium

# Stakers could become unable to exit all their locks if any of their locks are late exited multiple times

## Summary

## Vulnerability Detail
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L349-L371
```solidity
    function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
        locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }
```
In `exitLateById`, there is no validation that the lock hasn't already been exited. However, balances are still decreased for the staker. The global `lockedSupply` and `lockedSupplyWithMultiplier` can't be decreased past the total balance of the staker (due to underflow on their individual balance), but the staker's balances will be lower than expected if `exitLateById` is called multiple times on the same lock. This could occur during periods of high congestion where a staker may submit a transaction again due to lack of response in updating their unlock time (or even due to UX interactions). Consequently, the staker would be unable to exit all their locks due to underflow when subtracting from their balance. 

<details>
<summary>PoC</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.23;

import "forge-std/Test.sol";
import "./Setup.sol";
import "forge-std/console.sol";

contract Playground is Test, Setup {
    function testStake() public {
        vm.startPrank(user1);
        lock.stake(50e18, user1, 0);
        lock.stake(50e18, user1, 0);

        // double late exit on the same lock
        lock.exitLateById(0);
        lock.exitLateById(0);

        vm.expectRevert(stdError.arithmeticError);
        lock.exitLateById(1);

        assertEq(lock.getBalances(user1).locked, 0);

        vm.stopPrank();
    }
}
```
</details>

## Impact
Staker is unable to exit all their locks (ie. they lose the locked amount associated with the lock that can't be exited).

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L349-L371

## Tool used

Manual Review

## Recommendation
Verify that `lockedBalance.unlockTime == 0` (ie. it hasn't been set = exited already).