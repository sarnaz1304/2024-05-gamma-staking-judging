Upbeat Graphite Gorilla

medium

# Lack of slipppage parameters on `stake` will cause unintended locking periods

## Summary

When a user stakes some tokens (or restakes them), a parameter `typeIndex` is specified to get the locking period and the locking multipliers. If the admins change the locking periods and multipliers just before a user stakes, the user may end with an unintended locking period and multiplier.

This will lead to a loss of funds if the intended locking period is shorter than the updated period so the user has to call `earlyExitById` to get back the tokens and a penalty will be applied to that user. 

## Vulnerability Detail

When a user calls `stake` or `restakeAfterLateExit`, a parameter `typeIndex` must be specified to get the new locking period and the multiplier that will be applied to the new staked amount. The functions `stake` and `restakeAfterLateExit` will call `_stake`, which will get the multiplier and locking period based on the provided index:

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L263
```solidity
    function _stake(
        uint256 amount,
        address onBehalfOf,
>>      uint256 typeIndex,
        bool isRelock
    ) internal whenNotPaused {
        // ...

>>      uint256 multiplier = rewardMultipliers[typeIndex];
        bal.lockedWithMultiplier += amount * multiplier;
        lockedSupplyWithMultiplier += amount * multiplier;
        _updateRewardDebt(onBehalfOf);


        locklist.addToList(
            onBehalfOf, 
            LockedBalance({
                lockId: 0, // This will be set inside the addToList function
                amount: amount,
                unlockTime: 0, 
                multiplier: multiplier,
                lockTime: block.timestamp,
>>              lockPeriod: lockPeriod[typeIndex],
                exitedLate: false
            })
        );

        // ...
    }
```

The issue arises if the admins change the array of locking periods and multipliers by calling `setLockTypeInfo` just before a user stakes tokens. In this scenario, a user can end with the tokens locked for an unintended period of time, which is unfair for the user if the original intent was to stake the tokens for a shorter time period. 

This issue can also happen more frequently on Ethereum mainnet because a user's txn can be in the mempool for some time due to the rising gas prices so it's possible that when the txn is executed, the locking period array has already been updated. 

## Impact

When the admins update the arrays of locking periods and multipliers, it is probable that some users staking during that time will end up with an unintended time period and multiplier. In the worst case, a user could end with the tokens staked for 1 year instead of just 30 days. 

If this scenario is given and the user wants to recover the staked tokens, the function `earlyExitById` has to be called, leading to a penalty of up to 45% of the original staked tokens, which is a clear loss of funds for the user. 

## POC

Paste the following test in `RestakeAfterLateExit.t.sol` and run it with the following command `forge test --match-test test_lack_slippage`

```solidity
function test_lack_slippage() public {
    // A user is going to stake 100 tokens for 60 days (index 1)
    // When his txn is in the mempool, the admins change the locking configuration, removing the 60 day lock
    uint256[] memory newLockPeriod = new uint256[](2);
    uint256[] memory newLockMultiplier = new uint256[](2);
    newLockPeriod[0] = 30 days;
    newLockPeriod[1] = 360 days;
    newLockMultiplier[0] = 1;
    newLockMultiplier[1] = 1;
    vm.prank(deployer);
    lock.setLockTypeInfo(newLockPeriod, newLockMultiplier);

    // After that, the user's txn is included in a block
    vm.prank(user1);
    lock.stake(100e18, user1, 1);

    // We check the locking period of the user's stake
    // The lock period should be 60 days, but it's actually 360 days
    LockedBalance memory lockedBalance = lock.locklist().getLockById(user1, 0);
    assertEq(lockedBalance.lockPeriod, 360 days);
}
```

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L263

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended to add two slippage parameters in `_stake` so that the users can specify the expected multiplier and expected locking period. 