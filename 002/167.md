Formal Peach Fish

high

# High Gas Usage in withdrawAllUnlockedToken Function Leading to Potential DoS Attack

## Summary
The `Lock::withdrawAllUnlockedToken` function is vulnerable to a Denial of Service (DoS) attack due to its high gas consumption when processing a large number of locks. This could cause transactions to exceed the block gas limit, making it impossible for users with many locks to withdraw their tokens.

## Vulnerability Detail
The `Lock::withdrawAllUnlockedToken` function iterates through all locks of a user to determine which ones are unlocked and eligible for withdrawal. When a user has a large number of locks, the gas required to process all these locks can exceed the Ethereum block gas limit. As a result, the transaction will fail, preventing users from withdrawing their unlocked tokens.

This vulnerability can be exploited to prevent users from withdrawing their tokens by creating a large number of locks, either through legitimate staking or through an attack where locks are created with the intention of causing high gas usage.

## Impact
Users with a large number of locks will be unable to withdraw their tokens because the gas required to process all locks will exceed the block gas limit. This effectively locks their funds in the contract, leading to potential financial losses and reduced trust in the platform.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409C1-L430C6
```solidity
function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
        while (limit < lockCount) {
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```

## Proof Of Concept

Change the name of lockPeriod for example to lockPeriodValue like I did in the following function to avoid declaration shadows.

```solidity
 function calcPenaltyAmount(LockedBalance memory userLock) public view returns (uint256 penaltyAmount) {
        if (userLock.amount == 0) return 0; // Return zero if there is no amount locked to avoid unnecessary calculations.
        uint256 unlockTime = userLock.unlockTime;
        uint256 lockPeriodValue = userLock.lockPeriod;
        uint256 penaltyFactor;


        if (lockPeriodValue <= defaultRelockTime || (block.timestamp - userLock.lockTime) < lockPeriodValue) {

            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / lockPeriodValue + basePenaltyPercentage;
        }
        else {
            penaltyFactor = (unlockTime - block.timestamp) * timePenaltyFraction / defaultRelockTime + basePenaltyPercentage;
        }

        // Apply the calculated penalty factor to the locked amount.
        penaltyAmount = userLock.amount * penaltyFactor / WHOLE;
    }
```

```solidity
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriodValue = userLock.lockPeriod;
        
        if (lockPeriodValue <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriodValue) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
            return lockPeriodValue - (block.timestamp - lockTime) % lockPeriodValue;
        } else {
            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
            return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
        }
    }
```

And then paste this test in `Withdraw.t.sol`
Run  forge test --gas-limit 30000000 --mt testWithdrawAllUnlockedTokenDoS
This will set the gas limit to 30M which is the current gas limit per block.

```solidity
/// @notice Test if the withdrawAllUnlockedToken function can be DoS attacked due to a large lockCount
    function testWithdrawAllUnlockedTokenDoS() public {
        uint256 stakeAmount = 1 ether;
        uint256 numLocks = 150; // Adjust this to a very large number to ensure it exceeds the block gas limit

        // Fund the user with enough tokens
        _stakingToken.mint(user1, stakeAmount * numLocks);

        // Users stake multiple locks
        vm.startPrank(user1);
        _stakingToken.approve(address(lock), stakeAmount * numLocks);
        for (uint256 i = 0; i < numLocks; i++) {
            lock.stake(stakeAmount, user1, 0);
        }
        vm.stopPrank();

        // Warp to after unlock time for all locks
        vm.warp(block.timestamp + 31 days);

        // Log before withdraw
        uint256 user1BalanceBefore = _stakingToken.balanceOf(user1);
        emit log_named_uint("User1 balance before withdraw", user1BalanceBefore);

        // Attempt to withdraw all unlocked tokens, expecting it to fail
        vm.prank(user1);
        try lock.withdrawAllUnlockedToken() {
            fail();
        } catch {
            emit log("withdrawAllUnlockedToken exceeded block gas limit as expected");
        }
    }
```

## Tool used

Manual Review

## Recommendation
Implement a batch processing mechanism to handle the withdrawal of unlocked tokens in smaller chunks, or implement a tracking mechanism that keeps track of whether each token is locked or unlocked. When a token becomes unlocked, add it to a structure that holds all unlocked tokens for the user. The `withdrawAllUnlockedTokens` function will then only need to withdraw the value from this structure, which is more efficient and ensures that the gas cost remains manageable.