Uneven Admiral Urchin

medium

# Users can avoid early exit penalties since `exitLateById` lacks verification if `lockPeriod` has passed

## Summary
When users stake tokens, they can `exit early` or `exit late`.

If they `exit early`, they are returned their locked amount minus a `penalty`, which is sent to the `treasury`.

If they `exit late`, they can withdraw after their `cooldown` period has passed.

The problem is that a user never has to `exit early` and suffer the `penalty`, since `exiting late` does not check if their `lockPeriod` has passed. They can withdraw later without any penalties.

## Vulnerability Detail

After staking, user can exit early by calling `earlyExitById`
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313
```javascript
    /// @notice Allows a staker to perform an early exit from a locked position using the specified lock ID.
    /// @dev This function handles the early exit process, calculating penalties, updating balances, and transferring funds.
    /// @param lockId The unique identifier of the lock from which the user wishes to exit early.
    function earlyExitById(uint256 lockId) external whenNotPaused {
        if (isEarlyExitDisabled) {
            revert EarlyExitDisabled();
        }
        _updateReward(msg.sender);

        LockedBalance memory lock = locklist.getLockById(msg.sender, lockId);

        if (lock.unlockTime != 0)
            revert InvalidLockId();
        uint256 coolDownSecs = calcRemainUnlockPeriod(lock);
        lock.unlockTime = block.timestamp + coolDownSecs;
        uint256 penaltyAmount = calcPenaltyAmount(lock);
        locklist.removeFromList(msg.sender, lockId);
        Balances storage bal = balances[msg.sender];
        lockedSupplyWithMultiplier -= lock.amount * lock.multiplier;
        lockedSupply -= lock.amount;
        bal.locked -= lock.amount;
        bal.lockedWithMultiplier -= lock.amount * lock.multiplier;

        _updateRewardDebt(msg.sender);

@>     if (lock.amount > penaltyAmount) {
            IERC20(stakingToken).safeTransfer(msg.sender, lock.amount - penaltyAmount);
            IERC20(stakingToken).safeTransfer(treasury, penaltyAmount);
            emit EarlyExitById(lockId, msg.sender, lock.amount - penaltyAmount, penaltyAmount);
        } else {
            IERC20(stakingToken).safeTransfer(treasury, lock.amount);
        emit EarlyExitById(lockId, msg.sender, 0, penaltyAmount);
        }
    }
```

Although the protocol mentions that initially this will be toggled off, the protocol mentions in the README: `"In the future, we may turn on the ability to earlyExit"`.

As you can see, there is a `penaltyAmount` the user must pay for exiting early.

Alternatively, the user can call `exitLateById`.
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L349
```javascript
    function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
@>      locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }
```

After the `cooldown` period has passed, the user can proceed to withdraw:
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L437
```javascript
    function withdrawUnlockedTokenById(uint256 id) external nonReentrant {
        LockedBalance memory lockedBal = locklist.getLockById(msg.sender, id); // Retrieves the lock details for the specified ID.
        if (lockedBal.unlockTime != 0 && lockedBal.unlockTime < block.timestamp) {
            IERC20(stakingToken).safeTransfer(msg.sender, lockedBal.amount); // Transfers the unlocked amount to the user.
            locklist.removeFromList(msg.sender, id); // Removes the lock from the lock list.
            emit WithdrawUnlockedById(id, msg.sender, lockedBal.amount); // Emits an event logging the withdrawal of the unlocked tokens.
        }
    }
```

In this case, there is no penalty. In addition, there is no check within `exitLateById` to revert if the user is making an early exit.

The user can simply call `exitLateById` instead of `earlyExitById` to avoid the early exit penalties.

## Impact
Users can avoid early exit penalties after staking.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L349

## Tool used
Manual Review

## Recommendation
Add a check to `exitLateById` to ensure the user's lockperiod has passed:

```diff
    function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

+       uint256 lockTime = lockedBalance.lockTime;
+       uint256 lockPeriod = lockedBalance.lockPeriod;
+       require((block.timestamp - userLock.lockTime) >= lockPeriod, "Lock period has not yet passed");
```