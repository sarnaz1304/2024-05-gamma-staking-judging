Energetic Gingham Mouse

medium

# Incorrect implementation of `Lock.sol::withdrawAllUnlockedToken` increases the probability of DoS for people with multiple stakings

## Summary
Consider the function [`Lock.sol::withdrawAllUnlockedToken`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L404-L404)

The function is needed to withdraw unlocked tokens from all the user's stacks. The function is designed in such a way that it looks through all the user's stakings not in a single loop, but in pages of 10 stakings in one.

That is, the function will process the array [1,2,3...25] as follows: [1,2,....9], [10,..19], [20,..25]. In the function, the length of the array of all user's steakings is called lockCount. Thus, the complexity of the function is O((lockcount // 10) * 20) - linear (why 20 will become clearer later).

However, this is the assumed behaviour of the function. The implementation behaves differently because of a misprint. The function gets the following page for processing by calling [`LockList::getLocks`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/libraries/LockList.sol#L97-L97)

However, when [calling](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L411-L411) in the limit field, it passes LockCount (the length of all stakes) instead of the limit variable, which, if not passed to the function, turns into just a counter, 0, 10, 20 ... LockCount - which clearly contradicts both the name of the field and the meaning of the function.

Given this error, we calculate the complexity of the function.

A new page will still be requested LockCount // 10 times.
Calling `LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, LockCount)` for the first time will return us the entire list of the user's stakes, which we will process, however, the function will not finish executing and will continue iterating LockCount // 10 - 1 times; All these times `LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, LockCount)` will return an empty list of LockCount length. It is created in the line [`LockedBalance[] memory locks = new LockedBalance[](limit)`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/libraries/LockList.sol#L102-L102). Further along this empty list, the main function will be uselessly iterated

Thus, the complexity of function is O((lockCount // 10) * lockCount) - squared. This is an error in the implementation of the function, which can lead to Dos for users with numerous staking

## Vulnerability Detail
Ethereum has a limit of gas consumption per block, which is approximately 30 million. Therefore, if a transaction uses more than this amount, it cannot be included in the block. You can use such a simple test in Remix : These functions have exactly the same complexity as the functions in the contract.
```solidity
 function run_error(uint length) external pure {
        uint limit = 0;
        while(limit < length) {
            uint256[] memory arr = new uint256[](length);
            uint256 k = 0;
            for(uint i = 0; i < length; i++) {
                k += 1;
            }
            limit += 10;
        }
    }

    function run_correct(uint length) external pure {
        uint limit = 0;
        while(limit < length) {
            uint256[] memory arr = new uint256[](10);
            uint256 k = 0;
            for(uint i = 0; i < 10; i++) {
                k += 1;
            }
            for(uint i = 0; i < 10; i++) {
                k += 1;
            }
            limit += 10;
        }
    }
```
run_error stops working at a length of ~350 (in our contract, of course, this value will be even lower due to the fact that less gas is consumed at each iteration) While run_correct continues to run at a length of 5000.

## Impact
It is important to note that an incorrect implementation of the function does not break its functionality, so this error was not tracked by tests. However, such an implementation violates the intended one, and also significantly reduces the number of stakes that the user can have - otherwise Dos in this function.

Severity: Medium
Impact: Medium 

Rating: Medium
## Code Snippet
```solidity
function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
        while (limit < lockCount) {
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```
```solidity
function getLocks(
        address user,
        uint256 page,
        uint256 limit
    ) public view override returns (LockedBalance[] memory) {
        LockedBalance[] memory locks = new LockedBalance[](limit);
        uint256 lockIdsLength = lockIndexesByUser[user].length();

        uint256 i = page * limit;
        for (;i < (page + 1) * limit && i < lockIdsLength; i ++) {
            locks[i - page * limit]= lockById[lockIndexesByUser[user].at(i)];
        }
        return locks;
    }
```

## Tool used

Manual Review

## Recommendation
Fix the typo, call getLocks with limit instead of lockCount