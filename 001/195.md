Deep Pink Parrot

medium

# Users can exit with higher profits due to the incorrect implementation of the calcRemainUnlockPeriod function

## Summary

## Vulnerability Detail
There are several possible `lock periods`, each with its own `reward multiplier`. 
`Stakers` will receive more `rewards` when they `stake` for a `longer period`. 
In other words, the longer the `lock period`, the larger the `reward multiplier`.

Based on that, let me give an example.

Imagine the `defaultRelockTime` is `30 days`, and there are two `lock periods`. 
One is `30 days` with a `reward multiplier` of `1`, and the other is `100 days` with a `reward multiplier` of `4`. 
`Stakes` are automatically `restaked` for users unless they choose to `exit`.
`User A` `stakes` for the `100-day lock period`, while `User B` `stakes` the same amount for the `30-day lock period`. 
Whenever `rewards` are distributed, `User A` receives `4` times the `rewards` of `User B` due to the `longer lock period`.
`User A` wants to exit his `stake` on the `149th day`. 
Up to that point, he has been receiving `4` times the `rewards` because his `stake` was automatically `restaked` on the `101st day` for another `100-day period`. 
At this point, he should wait `51` more days to `unlock` his tokens, as he has been receiving `rewards` corresponding to the `100-day lock period`. 
However, due to the incorrect implementation of the `calcRemainUnlockPeriod` function, he can `withdraw` his tokens exactly on the `150th` day, just one day later.
```solidity
function exitLateById(uint256 id) external {
    uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
    locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);
}
```
Since the `lock period (100 days)` is longer than the `defaultRelockTime (30 days)` and the first `lock period` has already passed, the current `calcRemainUnlockPeriod` function returns just `1 day`.
```solidity
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
    uint256 lockTime = userLock.lockTime;  // 0
    uint256 lockPeriod = userLock.lockPeriod;  // 100
    
    if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
        return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
    } else {
        return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime; // 30 - (149 - 0) % 30 = 1
    }
}
```
As a result, `user A` can withdraw all his tokens with significant `rewards` just `1 day` later.
```solidity
function withdrawUnlockedTokenById(uint256 id) external nonReentrant {
    LockedBalance memory lockedBal = locklist.getLockById(msg.sender, id); // Retrieves the lock details for the specified ID.
    if (lockedBal.unlockTime != 0 && lockedBal.unlockTime < block.timestamp) {
        IERC20(stakingToken).safeTransfer(msg.sender, lockedBal.amount); // Transfers the unlocked amount to the user.
        locklist.removeFromList(msg.sender, id); // Removes the lock from the lock list.
        emit WithdrawUnlockedById(id, msg.sender, lockedBal.amount); // Emits an event logging the withdrawal of the unlocked tokens.
    }
}
```
## Impact
This is not a fair `rewards distribution`.
While I understood the intention of this protocol and believe that `restakeAfterLateExit` function is implemented correctly by considering the current `lock period` and `defaultRelockTime` appropriately, the issue lies in the fact that `rewards` have already stopped for the user when this function is called.
```solidity
function restakeAfterLateExit(uint256 id, uint256 typeIndex) external {
    if (currentLockPeriod <= defaultRelockTime || (block.timestamp - lockedBalance.lockTime) < currentLockPeriod) {
        require(newLockPeriod >= currentLockPeriod, "New lock period must be greater than or equal to the current lock period");
    } else {
        require(newLockPeriod >= defaultRelockTime, "New lock period must be greater than or equal to the default relock time");
    }
}
```
In the scenarios described above, the problem is that the user continues to benefit from higher `rewards`, so they should receive an appropriate `penalty` corresponding to those higher `rewards`.
## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L355-L356
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L596-L607
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L437-L444
## Tool used

Manual Review

## Recommendation
```solidity
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
      uint256 lockTime = userLock.lockTime;
      uint256 lockPeriod = userLock.lockPeriod;
      
-      if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) { 
-          return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
-      } else {
-          return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
-      }
+     return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
  }
```
Please fix `calcPenaltyAmount` function as well.

# I'm uncertain if this solution is appropriate, and it's essential for the sponsor to carefully consider the matter.