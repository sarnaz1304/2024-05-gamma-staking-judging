Upbeat Graphite Gorilla

medium

# DoS when notifying new rewards due to an out-of-bounds error

## Summary

The function `notifyUnseenReward` is called when a new reward has been sent to the contract and it needs to be distributed between the stakers. The function allows sending an array of reward tokens in order to be able to notify just a few reward tokens instead of all of them. 

However, there's a bug in the function that will cause it to revert every time the tokens to notify are less than the current reward tokens due to an index-out-of-bounds error. 

## Vulnerability Detail

When some tokens have been sent to the contract and they must be distributed between the stakers, the function `notifyUnseenReward` will be called:

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504-L511
```solidity
/// @notice Checks and updates unseen rewards for a list of reward tokens.
/// @dev Iterates through the provided list of reward tokens and triggers the _notifyUnseenReward function for each if it has been previously added to the contract.
/// @param _rewardTokens An array of reward token addresses to check and update for unseen rewards.
function notifyUnseenReward(address[] memory _rewardTokens) external {
    uint256 length = rewardTokens.length; // Gets the number of reward tokens currently recognized by the contract.
    for (uint256 i = 0; i < length; ++i) {
        if (rewardTokenAdded[_rewardTokens[i]]) {
            _notifyUnseenReward(_rewardTokens[i]); // Processes each token to update any unseen rewards.
        }
    }
}
```

The argument `_rewardTokens` is an array of tokens to check and update for unseen rewards. If the total reward tokens in the contract are 3 and the array is just one token, the function should just update the rewards for the specified token.

However, because of the bug in the function, when an array is passed with a length lower than the length of the reward tokens in the contract, the function will revert with an out-of-bounds error. The root cause is that the function is looping over all the reward tokens added to the contract instead of looping over the reward tokens provided as an argument. 

## Impact

When the `Lock` contract has more than one reward token added and anyone calls `notifyUnseenReward` to update a few reward tokens but not all of them, the function will revert, causing a DoS.

Given that the README states that the function `notifyUnseenReward` will be periodically called, the impact of this issue is quite significant. 

## PoC

Add this test to `Deposit.t.sol` and run the test with the following command `forge test --match-test test_notify_unseen_reward`. Before running the test, add this line on top of the file:

```solidity
import "../src/mock/MockToken.sol";
```

```solidity
function test_notify_unseen_reward() public {
    MockToken rewardToken2 = new MockToken("rewardToken2", "reward2", 18);

    // First, add a second reward token to the contract
    vm.startPrank(deployer);
    lock.addReward(address(rewardToken2));

    // Now, notify the contract of the new reward token
    rewardToken2.mint(address(lock), 1000e18);
    address[] memory notifyTokens = new address[](1);
    notifyTokens[0] = address(rewardToken2);
    vm.expectRevert(stdError.indexOOBError);
    lock.notifyUnseenReward(notifyTokens);
    // The function reverts with index-out-of-bounds error. 
}
```


## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L505

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended to loop over the array of reward tokens provided as an argument, and not over the array of reward tokens already added to the contract:

```diff
    function notifyUnseenReward(address[] memory _rewardTokens) external {
-       uint256 length = rewardTokens.length; // Gets the number of reward tokens currently recognized by the contract.
+       uint256 length = _rewardTokens.length;
        for (uint256 i = 0; i < length; ++i) {
            if (rewardTokenAdded[_rewardTokens[i]]) {
                _notifyUnseenReward(_rewardTokens[i]); // Processes each token to update any unseen rewards.
            }
        }
    }
```