Mythical Plum Snail

high

# Inconsistent Reward Calculation Leading to `Lock` Contract DoS

## Summary

The `_notifyReward()` function adds rewards without multiplying by `1e36`, while `_getReward()` attempts to decrement rewards divided by `1e36`. This inconsistency will lead to the contract eventually reverting due to an underflow error in `_notifyUnseenReward()`.

## Vulnerability Detail

In `_notifyReward()`, rewards are added directly:

```solidity
    function _notifyReward(address _rewardToken, uint256 reward) internal {
        if (lockedSupplyWithMultiplier == 0)
            return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).

        Reward storage r = rewardData[_rewardToken]; // Accesses the reward structure for the specified token.
        uint256 newReward = reward * 1e36 / lockedSupplyWithMultiplier; // Calculates the reward per token, scaled up for precision.
        r.cumulatedReward += newReward; // Updates the cumulative reward for the token.
        r.lastUpdateTime = block.timestamp; // Sets the last update time to now.
❌      r.balance += reward; // Increments the balance of the token by the new reward amount.
    }
```

However, in `_getReward()`, the reward balance is updated by dividing the reward by `1e36`:

```solidity
    function _getReward(
        address _user,
        address[] memory _rewardTokens
    ) internal whenNotPaused {
        uint256 length = _rewardTokens.length; // Get the number of reward tokens to process.
        for (uint256 i = 0; i < length; ) {
            address token = _rewardTokens[i]; // Get the current token address.

            uint256 reward = rewards[_user][token]; // Retrieve the amount of reward due for the user and the token.
            if (reward > 0) {
                rewards[_user][token] = 0; // Reset the reward to zero after claiming.
❌              rewardData[token].balance -= reward / 1e36; // Deduct the reward from the stored balance, adjusting for decimals.

                IERC20(token).safeTransfer(_user, reward / 1e36); // Transfer the reward to the user.
                rewardPaid[_user][token] += reward / 1e36; // Update the total reward paid to the user for this token.
                emit RewardPaid(_user, token, reward / 1e36); // Emit an event documenting the reward payment.
            }
            unchecked {
                i++;
            }
        }
    }
```

This causes a precision loss, preventing `rewardData[token].balance` from decrementing properly. Over time, `r.balance` keeps growing, leading to an underflow error in `_notifyUnseenReward()`:

```solidity
    function _notifyUnseenReward(address token) internal {
        if (token == address(0)) revert AddressZero(); // Ensures the token address is not zero.
        Reward storage r = rewardData[token]; // Accesses the reward data for the given token.
❌      uint256 unseen = IERC20(token).balanceOf(address(this)) - r.balance; // Calculates the amount of new, unseen rewards.

        if (unseen > 0) {
            _notifyReward(token, unseen); // Updates the reward data if there are new rewards.
        }

        emit NotifyUnseenReward(token, unseen); // Emits an event to log the notification of unseen rewards.
    }
```

## Impact

This issue prevents the proper decrement of `rewardData[token].balance`, causing it to grow indefinitely. Eventually, `_notifyUnseenReward()` will always revert due to an underflow error, making the contract unusable and rewards stuck.

## Code Snippet

- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L496
- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L549
- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L474

## Tool used

Manual Review

## Recommendation

Ensure consistency in reward calculations by multiplying the reward by `1e36` in `_notifyReward()` before adding it to the balance:

```diff
    function _notifyReward(address _rewardToken, uint256 reward) internal {
        if (lockedSupplyWithMultiplier == 0)
            return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).

        Reward storage r = rewardData[_rewardToken]; // Accesses the reward structure for the specified token.
        uint256 newReward = reward * 1e36 / lockedSupplyWithMultiplier; // Calculates the reward per token, scaled up for precision.
        r.cumulatedReward += newReward; // Updates the cumulative reward for the token.
        r.lastUpdateTime = block.timestamp; // Sets the last update time to now.
-       r.balance += reward; // Increments the balance of the token by the new reward amount.
+       r.balance += reward * 1e36;
    }
```