Rural Peanut Hawk

medium

# WithdrawAllUnlockedToken fails to execute when the lockCount exceeds 1700 due to out-of-gas

## Summary
The function is designed to allow users to withdraw all their unlocked tokens.
However, the current implementation leads to "out-of-gas" errors when a user has staked tokens more than 1700 times (lockCount > 1700).
This indicates an inefficiency in the withdrawal logic, causing excessive gas usage for processing large numbers of staked tokens.

## Vulnerability Detail
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L415
```solidity
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
        while (limit < lockCount) { // @audit doesn't need to iterate
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```

## Impact
Users who have staked tokens more than 1700 times are unable to withdraw all their unlocked tokens using the withdrawAllUnlockedToken function.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L415

## Tool used

Manual Review

## Recommendation
It's recommended to remove the while statement in the withdrawAllUnlockedToken function.
```diff
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
-        uint256 page;
-        uint256 limit;
        uint256 totalUnlocked;
        
-        while (limit < lockCount) {
-            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
+            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, 0, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

-            limit += 10; // Moves to the next page of locks.
-            page++;
-        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```