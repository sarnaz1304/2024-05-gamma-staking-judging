Mythical Plum Snail

high

# The `Lock` Contract Will Be Permanently DoSed When the `rewardTokens` Array Becomes Too Long

## Summary

Multiple functions loop over the `rewardTokens` array and will revert with an `Out of Gas error` when the array becomes too long, rendering the `Lock` contract unusable.

## Vulnerability Detail

The `rewardTokens` array is used to store the addresses of all reward tokens that can be distributed to users:

```solidity
    address[] public rewardTokens;
```

The reward token addresses are added by the `owner` with `Lock.addReward()`. There is no limit to the number of tokens that can be added to the array:

```solidity
    /// @notice Adds a new token to the list of reward tokens that will be distributed to stakers.
    /// @dev This function can only be called by the contract owner and checks for validity of the reward token before adding.
    /// @param _rewardToken The address of the token to be added as a reward token.
    function addReward(address _rewardToken) external override onlyOwner {
        if (_rewardToken == address(0)) revert InvalidBurn();
        if (rewardData[_rewardToken].lastUpdateTime != 0) revert AlreadyAdded();
        if (_rewardToken == stakingToken || rewardTokenAdded[_rewardToken]) revert InvalidRewardToken();
‚ùå      rewardTokens.push(_rewardToken);
        rewardTokenAdded[_rewardToken] = true;

        Reward storage reward = rewardData[_rewardToken];
        reward.lastUpdateTime = block.timestamp;
        emit AddReward(_rewardToken);
    }
```

Two functions loop over the entire `rewardTokens` array when a user wants to `stake()`, `earlyExitById()` and `exitLateById()`:

- `Lock._updateReward()`:

```solidity
    function _updateReward(address account) internal {
        uint256 length = rewardTokens.length; // Determine the number of reward tokens.
        Balances storage bal = balances[account]; // Access the balance record for the user.

        for (uint256 i = 0; i < length; ) {
            address token = rewardTokens[i]; // Access each token.
            Reward memory rewardInfo = rewardData[token]; // Get the reward data for the token.

            rewards[account][token] += _earned(account, token); // Update the rewards for the user based on what has been earned so far.
            rewardDebt[account][token] = rewardInfo.cumulatedReward * bal.lockedWithMultiplier; // Update the reward debt based on the latest reward information.

            unchecked {
                i++;
            }
        }
    }
```

- `Lock._updateRewardDebt()`:
```solidity
    function _updateRewardDebt(address _user) internal {
        Balances memory bal = balances[_user]; // Retrieve the current balance information for the user.

        for (uint i = 0; i < rewardTokens.length; ++i) {
            address rewardToken = rewardTokens[i]; // Access each reward token.
            Reward memory rewardInfo = rewardData[rewardToken]; // Get the current reward data for each token.

            // Recalculate the reward debt for the user based on their locked balances and the accumulated rewards for the token.
            rewardDebt[_user][rewardToken] = rewardInfo.cumulatedReward * bal.lockedWithMultiplier;
        }
    }
```

Note that there is no `removeReward()` so the `rewardTokens` array can only grow over time.

Therefore, when the `rewardTokens` array becomes too large, these functions will revert with an `out of gas error` when trying to loop over the array length, and the `Lock` contract will be unusable.

## Impact

Permanent DoS.

## Code Snippet

- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L614-L629
- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L636-L646

## Tool used

Manual Review

## Recommendation

Limit the maximum array length and add a `removeReward()` function.