Warm Felt Bear

medium

# Users' ````StakingToken```` would get stuck if ````exitLateById()```` called more than once with a same ````Id````

## Summary
Current implementation of ````exitLateById()```` allows repeatedly exiting a staking entry with a same ````Id````, this would cause users' other staking enties to get stuck.

## Vulnerability Detail
The issue arises due to lack of check if the ````Id```` has already been exited. And ````lockedSupply ````, ````lockedSupplyWithMultiplier ````, ````bal.lockedWithMultiplier```` and ````bal.locked```` (L360\~L364) would be wrongly updated.
```diff
File: src\Lock.sol
349:     function exitLateById(uint256 id) external {
350:         _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.
351: 
352:         LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.
+            if (lockedBalance .unlockTime != 0)
+               revert InvalidLockId();
353: 
354:         // Calculate and set the new unlock time based on the remaining cooldown period.
355:         uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
356:         locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);
357: 
358:         // Reduce the locked supply and the user's locked balance with and without multiplier.
359:         uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
360:         lockedSupplyWithMultiplier -= multiplierBalance;
361:         lockedSupply -= lockedBalance.amount;
362:         Balances storage bal = balances[msg.sender];
363:         bal.lockedWithMultiplier -= multiplierBalance;
364:         bal.locked -= lockedBalance.amount;
365: 
366:         locklist.setExitedLateToTrue(msg.sender, id);
367: 
368:         _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.
369: 
370:         emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
371:     }

```
The following PoC shows users' other staking entries would get stuck while this issue is triggered:
```solidity
pragma solidity 0.8.23;

import "forge-std/Test.sol";
import "./Setup.sol";

contract LateExitMoreThanOnceTest is Test, Setup {
    function test() public {
        // Users stake tokens
        vm.startPrank(user1);
        lock.stake(100e18, user1, 0); // id 0
        lock.stake(100e18, user1, 0); // id 1
        vm.stopPrank();
        
        assertEq(lockList.lockCount(user1), 2);
        LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
        assertEq(user1Locks[0].lockId, 0);
        assertEq(user1Locks[1].lockId, 1);

        vm.startPrank(user1);
        lock.exitLateById(0);
        // if call any of staking Id more than once, the other would be locked for ever
        lock.exitLateById(0);
        vm.stopPrank();

        vm.warp(block.timestamp + 30); // Simulate time passage

        vm.prank(user1);
        lock.withdrawUnlockedTokenById(0);

        vm.startPrank(user1);
        vm.expectRevert();
        lock.exitLateById(1);
        vm.expectRevert();
        lock.earlyExitById(1);
        vm.expectRevert();
        lock.restakeAfterLateExit(1, 0);
        vm.stopPrank();
    }
}
```

And the test log:
```solidity
2024-05-gamma-staking\StakingV2> forge test --mc LateExitMoreThanOnceTest -vv
[â †] Compiling...
No files changed, compilation skipped

Ran 1 test for test/LateExitMoreThanOnece.t.sol:LateExitMoreThanOnceTest
[PASS] test() (gas: 593134)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.29s (4.23ms CPU time)

Ran 1 test suite in 7.35s (7.29s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
````StakingToken```` might get stuck

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L349

## Tool used

Manual Review

## Recommendation
See ````Vulnerability Detail```` section
