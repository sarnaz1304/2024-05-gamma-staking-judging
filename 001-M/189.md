Sour Daffodil Koala

medium

# Withdrawing unlocked tokens can cost a lot of gas, could even lead to DOS for the user

## Summary
The `withdrawAllUnlockedTokens` could redundantly reiterate over array of length `lockCount` having empty `LockBalance`s. This could lead to very high gas costs for the user.

## Vulnerability Detail
The entire `while` loop is redundant because when `locklist.getLocks` is called the limit specified is `lockCount` which is the total number of locks of the user. So in the 1st iteration of `while` loop itself all locks are fetched and unlock balance is accounted for.
Remaining iterations (if any) would cost a lot of gas and could exceed block gas limit easily if a user has 100s or 1000s of locks.

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409-L430
```javascript
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
        
@>      while (limit < lockCount) {
@>         LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.
            page++;
        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```

## Impact
Redundant looping causing high gas cost leading to DOS for the particular user.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L409-L430

## Tool used

Manual Review

## Recommendation
Removing the `while` loop works exactly the same.
```diff
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
-       uint256 page;
-       uint256 limit;
        uint256 totalUnlocked;
        
-       while (limit < lockCount) {
-           LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount); // Retrieves a page of locks for the user.
+           LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, 0, lockCount); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

-           limit += 10; // Moves to the next page of locks.
-           page++;
-       }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```

PS: If the protocol intends to implement pagination, then the already implemented logic would lead to a Medium severity issue, due to skipping for locks because of shifting of indices of lock when calling `locklist.removeFromList` in the for loop. 