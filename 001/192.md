Sleepy Currant Ant

medium

# locks Get Relocked with `defaultRelockTime` But Their `multiplier` Remains Unchanged, Leading to Disproportionate Rewards

## Summary

The protocol incentivizes users to stake by offering rewards based on the amount and duration of staked tokens—the longer the lock period, the higher the multiplier for rewards. However, it includes a relocking mechanism where expired locks are automatically relocked for the minimum of `defaultRelockTime` or the original lock time. The issue arises because the multiplier from the initial lock period does not adjust to reflect this shorter relock period, allowing users to receive higher rewards for a reduced locking duration.

## Vulnerability Detail

users can stake with the function `stake` where they supply `typeIndex` that signifies the lockPeriod and multiplier chosen:

```solidity=260
function _stake(
        uint256 amount,
        address onBehalfOf,
        uint256 typeIndex,
        bool isRelock
    ) internal whenNotPaused {
        if (typeIndex >= lockPeriod.length || amount == 0) revert InvalidAmount();

        _updateReward(onBehalfOf);
        
        Balances storage bal = balances[onBehalfOf];

        bal.locked += amount;
        lockedSupply += amount;

=>      uint256 multiplier = rewardMultipliers[typeIndex];
        bal.lockedWithMultiplier += amount * multiplier;
        lockedSupplyWithMultiplier += amount * multiplier;
```

In line 275, the user's balance is multiplied by a `multiplier`, which is determined by the `lockPeriod`—the longer the `lockPeriod`, the larger the multiplier.

```solidity=281
locklist.addToList(
            onBehalfOf, 
            LockedBalance({
                lockId: 0, // This will be set inside the addToList function
                amount: amount,
                unlockTime: 0, 
                multiplier: multiplier,
                lockTime: block.timestamp,
=>              lockPeriod: lockPeriod[typeIndex],
                exitedLate: false
            })
        );
```

The protocol implements an automatic relocking mechanism where, upon expiration, locks are relocked for the minimum of `defaultRelockTime` or the original lock period:

```solidity=596
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        
        if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
=>          return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
        } else {
            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
=>          return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
        }
    }
```

The relock period (RelockPeriod) is used to determine when users can access their funds via `earlyExitById` and `exitLateById`. However, the protocol fails to update the lock's multiplier when it is relocked for a shorter duration. Consequently, a lock retains its high `multiplier` despite being relocked for just the `defaultRelockTime` (set to 30 days in tests, the smallest lockDuration accepted that has the smallest multiplier), which would normally correspond to a lower multiplier.

This oversight can lead to disproportionately high rewards for users who actually get relocked for a small lock time.

**Scenario**
- Assumptions
    - Lock periods and their multipliers are taken from the test file `UnlockCalculation.t.sol`
    - Default relock period is set to 30 days.
| Lock Period | Lock Multiplier |
|-------------|-----------------|
| 30 days     | 1               |
| 60 days     | 2               |
| 360 days    | 3               |

- attack Path
    - Bob locks 100 staking tokens for 360 days, receiving a lock multiplier of 3. Consequently, his `lockedWithMultiplier` equals 300
    - After 360 days, Bob is automatically relocked for the default relock time of 30 days but retains the `lockedWithMultiplier` of 300, with a multiplier of 3
    - He can unlock after each 30-day period, maintaining a lock multiplier of 3.
    - Alice locks 100 staking tokens for 30 days, receiving a lock multiplier of 1, thus her `lockedWithMultiplier` equals 100.
    - Despite having the same lock period now, Bob receives 3/4 of the rewards while Alice receives only 1/4.

## Proof Of Concept

here is a POC in foundry that does the following:

1. user 1 locks for 360 days 100 stakingToken
2. After 360 days have passed, check that User1 is relocked for 30 days but retains a multiplier from the 360-day lock period (3) instead of adjusting to 30 days (1).
3. user2 stakes 100 stakingToken for 30 days
4. deployer sends rewards to the contract
5. Rewards are distributed such that 3/4 go to Bob and 1/4 to Alice, even though they now have the same lock period.
6. after 30 days both Bob and Alice unstake
7. User3 stakes for 360 days and, after the period, chooses to exit late.
8. To retain the same multiplier, User3 would need to restake for another 360 days instead of just 30 days like user1

To test this scenario, add this function to the `UnlockCalculation.t.sol` test file and execute it using the following command: 

```bash
forge test --match-test testRewardMultiplierRelock -vv
```

```solidity
function testRewardMultiplierRelock() public {

        //user 1 stakes with 360 days lock
        vm.prank(user1);
        lock.stake(100e18, user1, 2);

        LockedBalance memory lockedBalance = lock.locklist().getLock(user1, 0);
        uint256 unlockPeriod = lock.calcRemainUnlockPeriod(lockedBalance) / 1 days;

        console.log("unlock period for user 1 before lockPeriod passed: ", unlockPeriod);

        uint256 lockMultiplier = lock.locklist().getLock(user1, 0).multiplier;

        console.log("lock multiplier for user 1 with lockTime of 360 days: ", lockMultiplier);

        // now pass the lock time of user 1
        vm.warp(block.timestamp + 360 days);

        //user1 has been relocked automatically with defaultLockTime = 30 days

        lockedBalance = lock.locklist().getLock(user1, 0);
        unlockPeriod = lock.calcRemainUnlockPeriod(lockedBalance) / 1 days;

        console.log("unlock period for user 1 after lockPeriod passed: ", unlockPeriod);

        // but the lock multiplier didn't change for that of 30 days, so he is still has multiplier for rewards like he is locked for 360 days
        lockMultiplier = lock.locklist().getLock(user1, 0).multiplier;

        console.log("lock multiplier for user 1 with lockTime of 30 days: ", lockMultiplier);

//-------------------------------------------------------------------------------------------------------------------------------------------

        //another user comes to stake
        vm.prank(user2);
        lock.stake(100e18, user2, 0); // User 2 stakes with 30 day lock

        // deployer sends 1000 reward token to the contract
        vm.startPrank(deployer);
        _rewardToken.transfer(address(lock), 1000e18);
        lock.notifyUnseenReward(rewardTokens);
        vm.stopPrank();

        //the rewards are split 3/4 for user 1 and 1/4 for user 2, although user1 and 2 have the same lockTime of 30 days
        vm.prank(user1);
        lock.getReward(rewardTokens);

        vm.prank(user2);
        lock.getReward(rewardTokens);

        console.log("User 1 reward: ", _rewardToken.balanceOf(user1));

        console.log("User 2 reward: ", _rewardToken.balanceOf(user2));

        //pass 30 days user 1 and user 2 can withdraw at the same time but user 1 having a bigger multiplier -> more rewards 
        //but for the same lockTime

        vm.warp(block.timestamp + 30 days);

        vm.startPrank(user1);
        lock.exitLateById(0);
        lock.withdrawAllUnlockedToken();
        vm.stopPrank();

        vm.startPrank(user2);
        lock.exitLateById(1);
        lock.withdrawAllUnlockedToken();
        vm.stopPrank();

//-----------------------------------------------------------------------------------------------------------------------------------------

        // user 3 stakes with period 360 days
        vm.prank(user3);
        lock.stake(100e18, user3, 2);

        vm.warp(block.timestamp + 360 days);

        //user3 late exits
        vm.startPrank(user3);
        lock.exitLateById(2);

        //user 3 after late exiting, can restake but to restake with the same multiplier he will have to lock for the same duration
        lock.restakeAfterLateExit(2,2);
        vm.stopPrank();

        // after relocking user 3 will have to lock for 360 days to get the same reward multiplier
        lockedBalance = lock.locklist().getLock(user3, 0);
        unlockPeriod = lock.calcRemainUnlockPeriod(lockedBalance) / 1 days;

        console.log("lock period for user 3: ", unlockPeriod);

    }
```
    
## Impact

The current relocking mechanism permits users to retain a high reward multiplier even when their tokens are relocked for significantly shorter periods. This flaw allows users to accrue substantially more rewards than intended by the protocol's design, distorting the reward distribution mechanism and undermining the fairness and economic balance of the system.

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L260-L307

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L349-L371

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L596-L607

## Tool used

Manual Review

## Recommendation

make it so that locks get relocked for the same period after it ends:

```diff=596
function calcRemainUnlockPeriod(LockedBalance memory userLock) public view returns (uint256) {
        uint256 lockTime = userLock.lockTime;
        uint256 lockPeriod = userLock.lockPeriod;
        
-        if (lockPeriod <= defaultRelockTime || (block.timestamp - lockTime) < lockPeriod) {
            // If the adjusted lock period is less than or equal to the default relock time, or if the current time is still within the adjusted lock period, return the remaining time based on the adjusted lock period.
            return lockPeriod - (block.timestamp - lockTime) % lockPeriod;
-} else {
-            // If the current time exceeds the adjusted lock period, return the remaining time based on the default relock time.
-            return defaultRelockTime - (block.timestamp - lockTime) % defaultRelockTime;
-        }            
```

and make a function the users will need to call if they want to relock for another period that sets the multiplier accordignly.