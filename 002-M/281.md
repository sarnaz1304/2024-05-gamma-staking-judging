Upbeat Graphite Gorilla

medium

# `exitLateById` can be called more than once per lock

## Summary

The function `exitLateById` is called when a user wants a stop the lock of some staking tokens when the current period finishes. The issue is that the function can be called more than once per lock, and that will lead to stuck rewards and griefing if a smart contract integrates with the protocol. 

## Vulnerability Detail

When a user wants to stop the lock of some staking tokens, the function `exitLateById` should be called:

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L349-L371
```solidity
function exitLateById(uint256 id) external {
    _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

    LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id); // Retrieves the lock details from the lock list as a storage reference to modify.

    // Calculate and set the new unlock time based on the remaining cooldown period.
    uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
    locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

    // Reduce the locked supply and the user's locked balance with and without multiplier.
    uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
    lockedSupplyWithMultiplier -= multiplierBalance;
    lockedSupply -= lockedBalance.amount;
    Balances storage bal = balances[msg.sender];
    bal.lockedWithMultiplier -= multiplierBalance;
    bal.locked -= lockedBalance.amount;

    locklist.setExitedLateToTrue(msg.sender, id);

    _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

    emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
}
```

Any user that has more than one lock can call this function more than once using the same lock. That will cause the user's balance to unfairly decrease twice for the same lock and will lead to griefing when that user is a smart contract interacting with the protocol.

When a smart contract interacts with the Gamma protocol and allows the users to create and exit locks, the users will be able to call twice `exitLateById` and grief the rest of the users using that smart contract. That is possible because as long as the user has enough balance, that balance can be zeroed if that user calls repeatedly `exitLateById` using the same lock. 

This weird behavior will for sure have unintended consequences when external smart contracts interact with Gamma and will lead to clear griefing and a loss of funds. 

## Impact

A user can call `exitLateById` more than once using the same lock, and that will cause the balance to decrease infinitely until it arrives at zero. This behavior will cause users with stuck locks they cannot withdraw and they cannot receive rewards, so users affected by this issue won't be able to recover the staked funds affected by the bug. 

## Code Snippet

https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L349-L371

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended that the function `exitLateById` can only be called once per lock:

```diff
    function exitLateById(uint256 id) external {
        _updateReward(msg.sender); // Updates any pending rewards for the caller before proceeding.

        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id);
+       require(lockedBalance.unlockTime == 0);        

        // Calculate and set the new unlock time based on the remaining cooldown period.
        uint256 coolDownSecs = calcRemainUnlockPeriod(lockedBalance);
        locklist.updateUnlockTime(msg.sender, id, block.timestamp + coolDownSecs);

        // Reduce the locked supply and the user's locked balance with and without multiplier.
        uint256 multiplierBalance = lockedBalance.amount * lockedBalance.multiplier;
        lockedSupplyWithMultiplier -= multiplierBalance;
        lockedSupply -= lockedBalance.amount;
        Balances storage bal = balances[msg.sender];
        bal.lockedWithMultiplier -= multiplierBalance;
        bal.locked -= lockedBalance.amount;

        locklist.setExitedLateToTrue(msg.sender, id);

        _updateRewardDebt(msg.sender); // Recalculates reward debt after changing the locked balance.

        emit ExitLateById(id, msg.sender, lockedBalance.amount); // Emits an event logging the details of the late exit.
    }
```