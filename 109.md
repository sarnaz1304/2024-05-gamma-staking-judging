Mini Rusty Coyote

high

# Staked `GAMMA` amounts are distributed as rewards leading to users unable to withdraw

## Summary
Staked `GAMMA` tokens will be counted as `unseenRewards` and will be distributed to every participant leading to protocol's accounting mechanisms to break and users unable to withdraw their staked funds

## Vulnerability Detail
First of all, according to team's statements, they have the intention to use `GAMMA` token as both `stakingToken` and also one of the `rewardTokens` together with WETH.

<details>
<summary> Statements </summary>
<img width="612" alt="Screenshot 2024-05-20 at 08 25 36" src="https://github.com/sherlock-audit/2024-05-gamma-staking-zarkk01/assets/77061323/d930ed4b-0b89-4211-b6b4-63b1b7e69a24">
<img width="613" alt="Screenshot 2024-05-20 at 08 23 45" src="https://github.com/sherlock-audit/2024-05-gamma-staking-zarkk01/assets/77061323/69f0c677-c2ca-4601-94e2-e990f37c123f">
<img width="813" alt="Screenshot 2024-05-20 at 08 23 11" src="https://github.com/sherlock-audit/2024-05-gamma-staking-zarkk01/assets/77061323/2948837f-7b33-45ad-8630-12c54ae0ff60">
</details>

At first glance, this seems impossible due to [this](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L175) check on `addReward` function but this can easily be bypassed if they just add the rewards first and then set the staking token.  The bug has to do with the fact that when the rewards are counted by calling the `notifyUnseenReward()` the `GAMMA` balance of the `Lock` contract will contain both the staked amounts and the amounts that are indeed for distribution purposes. However, the functions logic(as shown in the Code Snippet) does not check for this and assumes that whatever `GAMMA` is "inside" the contract is about to be distributed as reward. This, of course, will break the protocol's accounting and will have as result all of the `GAMMA` tokens to be given as reward. As a result, stakers will not be able to withdraw their funds since they are distributed.  But, even if some stakers withdraw their `GAMMA` that means that other users will not receive their rewards. To understand better the bug, add this test suite in `test` file and run `forge test --mt testFunctionality` :

```solidity
pragma solidity 0.8.23;

import {Test, console} from "forge-std/Test.sol";
import "../src/Lock.sol";
import "../src/mock/MockToken.sol";
import "../src/libraries/LockList.sol";

contract PoC is Test {
    Lock lock;
    LockList lockList;
    address deployer;
    address user1;
    address user2;
    address user3;
    MockToken _stakingToken;
    MockToken _rewardToken;
    uint256[] lockPeriod;
    uint256[] lockMultiplier;
    address[] rewardTokens;

    function setUp() public {
        user1 = vm.addr(1);
        user2 = vm.addr(2);
        user3 = vm.addr(3);
        deployer = vm.addr(9999999);
        uint256 forkId = vm.createFork("https://eth-mainnet.g.alchemy.com/v2/VZH1EtzO3RVWYSXs523zfhFzlO6KHnr6");
        vm.selectFork(forkId);

        vm.startPrank(deployer);
        lock = new Lock();
        lockList = new LockList(deployer);
        
        // Initialize the Lock contract with necessary configurations
        lock.initialize(address(lockList), 15000, 35000, deployer);
        _stakingToken = new MockToken("stakingToken", "stkToken", 18);
        _rewardToken = _stakingToken;
        rewardTokens.push(address(_rewardToken));

        // Configure reward token and staking token settings
        lock.addReward(address(_rewardToken));
        lock.setStakingToken(address(_stakingToken));
        lockPeriod.push(10);
        lockPeriod.push(20);
        lockPeriod.push(30);
        lockMultiplier.push(1);
        lockMultiplier.push(2);
        lockMultiplier.push(3);
        lock.setLockTypeInfo(lockPeriod, lockMultiplier);

        // Transfer ownership of locker lists to Lock contract
        _stakingToken.mint(user1, 500e18);
        _stakingToken.mint(user2, 500e18);
        _stakingToken.mint(user3, 500e18);
        _rewardToken.mint(deployer, 100000e18);
        lock.setTreasury(address(0x4));
        lockList.transferOwnership(address(lock));
        vm.stopPrank();

        // Approve the Lock to manage staking tokens on behalf of users
        vm.prank(user1);
        _stakingToken.approve(address(lock), 10000e18);
        vm.prank(user2);
        _stakingToken.approve(address(lock), 10000e18);
        vm.prank(user3);
        _stakingToken.approve(address(lock), 10000e18);
        vm.prank(deployer);
        lock.setDefaultRelockTime(10);
    }

    function testFunctionality() public {
        // User 1 stakes 500 gamma with 1 multiplier
        vm.prank(user1);
        lock.stake(500e18, user1, 0);

        // User 2 stakes 500 gamma with 1 multiplier
        vm.prank(user2);
        lock.stake(500e18, user2, 0);

        // User 3 stakes 500 gamma with 1 multiplier
        vm.prank(user3);
        lock.stake(500e18, user3, 0);
        
        // Deployer add rewards and notify
        vm.startPrank(deployer);
        _rewardToken.transfer(address(lock), 10000e18);
        address[] memory rewardTokensForNotify = new address[](1);
        rewardTokensForNotify[0] = address(_rewardToken);
        lock.notifyUnseenReward(rewardTokensForNotify);
        vm.stopPrank();

        // User 1 gets rewards
        vm.prank(user1);
        lock.getAllRewards();
        
        // User 2 gets rewards
        vm.prank(user2);
        lock.getAllRewards();
        
        // User 3 gets rewards
        vm.prank(user3);
        lock.getAllRewards();

        // User 1 trys to early exit (he could try lateExit and withdrawLate too)
        vm.prank(user1);
        vm.expectRevert();
        // Balance of tokens in the contract is not enough since they are distributed as rewards, so this will revert
        lock.earlyExitById(0);
    }
}
```

<details>
<summary> Console output </summary>
As you will see, the balance will be insufficient.
<img width="1144" alt="Screenshot 2024-05-20 at 08 50 45" src="https://github.com/sherlock-audit/2024-05-gamma-staking-zarkk01/assets/77061323/d60ae422-1fa4-41c9-89bf-b59acae28bb9">
</details>

## Impact
Users will be unable to withdraw their staked `GAMMA` amount since it will have be distributed as rewards to every participant. Also, the whole protocol's reward distribution mechanism will be considered broken since the staked amounts are blended with the reward amounts and, in the end, either someone who wish to receive his rewards or someone who wants to exit will not be able to do so.

## Code Snippet
This is the vulnerable code snippet where the balance of `GAMMA` tokens is used for counting rewards and therefore will be distrbuted :

```solidity
    function _notifyUnseenReward(address token) internal {
        if (token == address(0)) revert AddressZero(); // Ensures the token address is not zero.
        Reward storage r = rewardData[token]; // Accesses the reward data for the given token.
        // @audit In the balanceOf(), there is also the staked amounts.
@>        uint256 unseen = IERC20(token).balanceOf(address(this)) - r.balance; // Calculates the amount of new, unseen rewards.

        if (unseen > 0) {
            _notifyReward(token, unseen); // Updates the reward data if there are new rewards.
        }

        emit NotifyUnseenReward(token, unseen); // Emits an event to log the notification of unseen rewards.
    }
```
[Link to code](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L471)

## Tool used
Manual code inspection, Solodit

## Recommendation
Consider not using `GAMMA` token for both staking and rewards.