Keen Flaxen Quail

medium

# User can miss rewards

## Summary
The user can miss the distribution of rewards if he is the only staker in the contract.

## Vulnerability Detail
Consider the following scenario:
1. The owner transfers reward assets to Lock.sol and calls [`notifyUnseenReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504)
2. The [`_notifyReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L488-L497) will not update the rewards because there are **no active stakes in the contract currently**:

```solidity
    function _notifyReward(address _rewardToken, uint256 reward) internal {
@>      if (lockedSupplyWithMultiplier == 0)
            return; // If there is no locked supply with multiplier, exit without adding rewards (prevents division by zero).


        Reward storage r = rewardData[_rewardToken];
        uint256 newReward = reward * 1e36 / lockedSupplyWithMultiplier;
        r.cumulatedReward += newReward;
        r.lastUpdateTime = block.timestamp;
        r.balance += reward;
    }
```
3. User stakes X amount of staking token - he is eligible to get all rewards but to get them he needs to call [`notifyUnseenReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504) - note: it is not said anywhere that this function **must and will be called by the user or by any keeper**. It is only said that it will be called periodically by the protocol after sending funds like they already did in **step 1** above.
4. User calls [`getAllRewards()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L517) - result no reward tokens are distributed.
5. As the protocol decides that it is acceptable for the protocol to function with step-wise jumps the user can decide to exit his stake with either [`exitLateById()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L349) or [`earlyExitById()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L313) and still be eligible for the rewards despite the time spent being staked.
6. After the user exits, 2 other users stake, and [`notifyUnseenReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504) gets called
7. When these 2 users call [`getAllRewards()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L517) they will take all rewards and user 1 will **not get any rewards still**.

## Impact
The first staker can lose rewards.

I consider this a valid Medium because there is no guarantee of the order of calling the functions mentioned above. In the contest docs, it was not mentioned: "The reward tokens will be sent periodically to the staking contract and function notifyUnseenRewards() will be called." However, this is not enough to cover the case described above. 

## Code Snippet
Place the test inside Deposit.t.sol:

```solidity
function test_poc_user_no_rewards() public {
        // Distributing rewards to the staking contract
        vm.prank(deployer);
        _rewardToken.transfer(address(lock), 600e18);
        vm.prank(deployer);
        lock.notifyUnseenReward(rewardTokens);
        assertEq(_rewardToken.balanceOf(address(lock)), 600e18);

        (uint256 lastUpdateTime, uint256 cumulatedReward, uint256 balance) = lock.rewardData(address(_rewardToken));
        assertEq(cumulatedReward, 0);
        assertEq(balance, 0);

        // This staker is eligable to get rewards as he is the first one to stake
        vm.prank(user1);
        lock.stake(100e18, user1, 0);

        vm.prank(user1);
        lock.getAllRewards();
        assertEq(_rewardToken.balanceOf(user1), 0); // user is not getting any rewards

        // He late exits
        vm.prank(user1);
        lock.exitLateById(0);

        vm.prank(user1);
        lock.getAllRewards();
        assertEq(_rewardToken.balanceOf(user1), 0); // user is not getting any rewards because he exited without calling notifyUnseenReward

        // 2 other users stake
        vm.prank(user2);
        lock.stake(100e18, user2, 0);
        vm.prank(user3);
        lock.stake(100e18, user3, 0);

        // Somebody calls notifyUnseenReward
        lock.notifyUnseenReward(rewardTokens);

        vm.prank(user1);
        lock.getAllRewards();
        assertEq(_rewardToken.balanceOf(user1), 0); // User 1 gets 0 rewards

        vm.prank(user2);
        lock.getAllRewards();
        assertEq(_rewardToken.balanceOf(user2), 300e18);

        vm.prank(user3);
        lock.getAllRewards();
        assertEq(_rewardToken.balanceOf(user3), 300e18);
    }
```

## Tool used

Manual Review

## Recommendation
Create a modifier that will execute the logic inside [`notifyUnseenReward()`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L504) before each stake/withdraw.
