Cool Pecan Otter

medium

# No slippage check for `rewardMultipliers`/`lockPeriod` for `stake()`/`restakeAfterLateExit()` functions


## Summary

When users are staking or restaking, the global `rewardMultipliers[]` and `lockPeriod[]` are used to create the lock struct. However, both `rewardMultipliers[]` and `lockPeriod[]` are change-able by Admin. Due to the nature of blockchain, the staking transaction and admin set transaction may come in different orders, causing the user to stake with unexpected parameters.

## Vulnerability Detail

An example is:

1. User submits a stake transaction with typeIndex == 0. Currently rewardMultipliers[0] is 1, lockPeriod[0] is 30 days.
2. Admin changes rewardMultipliers[0] to 10, lockPeriod[0] to 360 days.
3. Transaction 2 is executed before transaction 1, due to the nature of blockchain.
4. User now has his tokens locked for 360 days, which was not his intent.

```solidity
    function setLockTypeInfo(
        uint256[] calldata _lockPeriod,
        uint256[] calldata _rewardMultipliers
    ) external onlyOwner {
        if (_lockPeriod.length != _rewardMultipliers.length)
            revert InvalidLockPeriod();
        delete lockPeriod;
        delete rewardMultipliers;
        uint256 length = _lockPeriod.length;
        for (uint256 i; i < length; ) {
            lockPeriod.push(_lockPeriod[i]);
            rewardMultipliers.push(_rewardMultipliers[i]);
            unchecked {
                i++;
            }
        }

        emit SetLockTypeInfo(lockPeriod, rewardMultipliers);
    }

    function _stake(
        uint256 amount,
        address onBehalfOf,
        uint256 typeIndex,
        bool isRelock
    ) internal whenNotPaused {
        if (typeIndex >= lockPeriod.length || amount == 0) revert InvalidAmount();

        _updateReward(onBehalfOf);
        
        Balances storage bal = balances[onBehalfOf];

        bal.locked += amount;
        lockedSupply += amount;

>       uint256 multiplier = rewardMultipliers[typeIndex];
        bal.lockedWithMultiplier += amount * multiplier;
        lockedSupplyWithMultiplier += amount * multiplier;
        _updateRewardDebt(onBehalfOf);


        locklist.addToList(
            onBehalfOf, 
            LockedBalance({
                lockId: 0, // This will be set inside the addToList function
                amount: amount,
                unlockTime: 0, 
                multiplier: multiplier,
                lockTime: block.timestamp,
>               lockPeriod: lockPeriod[typeIndex],
                exitedLate: false
            })
        );

        if (!isRelock) {
            IERC20(stakingToken).safeTransferFrom(
                msg.sender,
                address(this),
                amount
            );
        }

        emit Locked(
            onBehalfOf,
            amount,
            balances[onBehalfOf].locked
        );
    }

    function restakeAfterLateExit(uint256 id, uint256 typeIndex) external {
        // Retrieve the lock details for the specified ID.
        LockedBalance memory lockedBalance = locklist.getLockById(msg.sender, id);
        require(lockedBalance.exitedLate, "This lock was not exited late or is ineligible for restaking.");

>       uint256 newLockPeriod = lockPeriod[typeIndex]; // Get the new lock period based on the type index.
        uint256 currentLockPeriod = lockedBalance.lockPeriod;

        // Enforce that the new lock period must be valid based on the current conditions.
        if (currentLockPeriod <= defaultRelockTime || (block.timestamp - lockedBalance.lockTime) < currentLockPeriod) {
            require(newLockPeriod >= currentLockPeriod, "New lock period must be greater than or equal to the current lock period");
        } else {
            require(newLockPeriod >= defaultRelockTime, "New lock period must be greater than or equal to the default relock time");
        }

        // Proceed to restake the funds using the new lock type.
>       _stake(lockedBalance.amount, msg.sender, typeIndex, true);

        // Remove the old lock record to prevent any further operations on it.
        locklist.removeFromList(msg.sender, id);

        emit RestakedAfterLateExit(msg.sender, id, lockedBalance.amount, typeIndex);
    }

```


## Impact

Users may end up staking or restaking with unexpected `rewardMultipliers` and `lockPeriod`.

## Code Snippet

- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L124-L147
- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L260
- https://github.com/sherlock-audit/2024-05-gamma-staking/blob/main/StakingV2/src/Lock.sol#L378

## Tool used

Manual review

## Recommendation

Also pass in the expected `rewardMultipliers` and `lockPeriod` for staking functions, and check it is equal to `rewardMultipliers[typeIndex]`, `lockPeriod[typeIndex]`.
