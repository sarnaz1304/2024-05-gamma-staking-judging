Formal Taupe Wasp

medium

# Lock.withdrawAllUnlockedToken can be DOSed

## Summary
According to the contest README, [Lock.withdrawAllUnlockedToken`](https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L409-L430) is a core functionality allowing stakers to withdraw tokens from all eligible locks. There is a problem with that function, as it is relatively easy for anyone to DOS it.

## Vulnerability Detail
When the function calls `locklist.getLocks()` it wrongly passes `lockCount` instead of `limit`.
```solidity
    function withdrawAllUnlockedToken() external override nonReentrant {
        uint256 lockCount = locklist.lockCount(msg.sender); 
        while (limit < lockCount) {
           // @audit should pass limit, not lockCount
            LockedBalance[] memory lockedBals = locklist.getLocks(msg.sender, page, lockCount);
            for (uint256 i = 0; i < lockedBals.length; i++) {
                if (lockedBals[i].unlockTime != 0 && lockedBals[i].unlockTime < block.timestamp) {
                    totalUnlocked += lockedBals[i].amount;
                    locklist.removeFromList(msg.sender, lockedBals[i].lockId);
                }
            }

            limit += 10; 
            page++;
        }
    }
```

Then, `LockList.getLocks()` will use the length of the whole array as `limit`. This will result in processing all the locks in the first iteration of the while loop in `withdrawAllUnlockedToken`. However, the loop won't stop there. It will increase the `limit` with 10 and will continue caling getLocks. For each of these calls `getLocks` will return an empty array with length of all the locks. This will trigger the nested for loop and `length` iterations will be made again. 
```solidity
    function getLocks(
        address user,
        uint256 page,
        uint256 limit
    ) public view override returns (LockedBalance[] memory) {
        LockedBalance[] memory locks = new LockedBalance[](limit);
        uint256 lockIdsLength = lockIndexesByUser[user].length();

        uint256 i = page * limit;
        for (;i < (page + 1) * limit && i < lockIdsLength; i ++) {
            locks[i - page * limit]= lockById[lockIndexesByUser[user].at(i)];
        }
        return locks;
    }
```

As a result, for any locks with `length = n`, `withdrawAllUnlockedToken` will make $\frac{n^2}{10}$ iterations.

A malicious user can easily increase the number of `n` to a very large number by staking on the behalf of another user. There is no floor amount for staking, so he can spam a lot of 1 wei stakes. 


## Impact
Because the function has to execute $\frac{n^2}{10}$ iterations and n can be increased to a very large value, the gas costs will spike to a such large value that using withdrawAllUnlockedToken may become impossible.

## Code Snippet
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L409-L430
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/libraries/LockList.sol#L97-L110
https://github.com/sherlock-audit/2024-05-gamma-staking/blob/703fd3604069489937037f20490ec8c492c0508e/StakingV2/src/Lock.sol#L260-L307
## Tool used

Manual Review

## Recommendation
Pass the correct limit in the for loop. Also, a staking floor amount may be added.